#+TITLE: README
#+LATEX_CLASS: article
#+OPTIONS: toc:nil
#+STARTUP: show2levels

[[https://travis-ci.org/kimim/kimim-emacs][https://travis-ci.org/kimim/kimim-emacs.svg]]

* Introduction

This Emacs configuration file is written in literate programming
method [fn:1].  The =emacs-lisp= code is extracted from this
=orgmode= [fn:2] file, and then compiled to binary =elc= file for fast
loading in the future.

* Installation

First of all, you need to get =emacs= and install it in your machine.

** Windows/MSYS2

On Windows, we have three options: 1) MSYS2, 2) Cygwin, 3) WSL2.

If your msys2 [fn:3] is installed in =C:\msys64=, you can open
=C:\msys64\mingw64.exe=, and run following command to install Emacs:

#+begin_src shell
pacman -S git mingw-w64-x86_64-emacs
#+end_src

** Windows/Cygwin

Since 2020, I use MSYS2 MingW64 Emacs for most of the time, Cygwin
Emacs is not actively tested now, but Emacs should work in Cygwin
environment smoothly.

Cygwin project[fn:4] is a large collection of GNU and Open Source
tools provide functionality similar to a Linux distribution on
Windows.

You can download and install Cygwin setup [fn:5] in your machine, for
example, at =C:\cygwin64=. Then you can use =mintty.exe= to work with
=bash=.

Then start =emacs= from cygwin terminal, and right click the =emacs= icon
to pin it to taskbar, then click the property of this icon to add
bellow to the target field. This way will hide the black terminal.

#+begin_src bat
C:\cygwin64\bin\run.exe emacs-w32
#+end_src

You can also add =emacs --daemon= in your scheduled task, and change the
target field as:

#+begin_src bat
C:\cygwin64\bin\run.exe emacsclient -c
#+end_src

This will attach the client to the server daemon. You can get a
running =emacs= very quick.

** Windows/WSL2

Nowadays, Windows is embracing Linux tightly. You can try =emacs= on
Windows subsystem for Linux version 2. You can get Ubuntu Linux from
Windows App Store or Manjaro Linux for WSL2 [fn:6].

Commands to install Emacs is same as on Linux in next section.

** Linux

Most of the external tools I used in this Emacs configuration should
be easily installed or already available in main Linux distributions.

For example, in Ubuntu, you can install this way:

#+begin_src shell
sudo snap install emacs --classic
#+end_src

It is also very quick to add emacs in ArchLinux/Manjaro:

#+begin_src shell
pacman -S emacs
#+end_src

** macOS

For Apple macOS, most UNIX tools are installed already. You can use
homebrew [fn:7] to install additional application if it is missing.

#+begin_src shell
/bin/bash -c \
"$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install.sh)"
brew tap railwaycat/emacsmacport
brew install emacs-mac
#+end_src

* Preparation
** Get emacs config

You can follow below shell commands to prepare the =emacs= configuration
files and folders:

#+begin_src shell
# backup existing emacs config
cd ~ && mv .emacs .emacs-backup && mv .emacs.d .emacs.d-backup
# clone this config
git clone https://github.com/kimim/kimim-emacs
# copy default .emacs to ~
cp kimim-emacs/.emacs ~
#+end_src

This configuration uses several path to keep different information,
you need to define them in =~/.emacs=:

- =kimim/path-sync= is the root of sync folder
- =kimim/path-sync-emacs= to sync emacs settings
- =kimim/path-org= to sync todo list and journals
- =kimim/path-notes= to sync notes
- =kimim/path-docs= to keep reference documents
- =kimim/path-kimim-emacs= where kimim-emacs you clone to

Let's tell =emacs=, if the path is not set, report error, if the path is
set, but not exists, create it.

#+begin_src emacs-lisp
(mapc (lambda (path)
        (if (not (boundp path))
            (error (concat "please set " (symbol-name path) " in ~/.emacs"))
          (if (not (file-exists-p (symbol-value path)))
              (make-directory (symbol-value path)))))
      '(kimim/path-sync
        kimim/path-sync-emacs
        kimim/path-org
        kimim/path-notes
        kimim/path-docs
        kimim/path-kimim-emacs))
#+end_src

Then you can execute =emacs= to bootstrap itself.

** chemacs2

Another way to try this configure out is to use [[https://github.com/plexus/chemacs2][chemacs2]].

Clone to ~~/kimim-emacs~

#+begin_src shell
git clone https://github.com/kimim/kimim-emacs ~/kimim-emacs
#+end_src

Add to chemacs2 profile file ~~/.emacs.profile.el~

#+begin_src emacs-lisp :tangle no
(("default" . ((user-emacs-directory . "~/.emacs.default")))
 ("kimim" . ((user-emacs-directory . "~/kimim-emacs"))))
#+end_src

Start emacs with this config:

#+begin_src shell
emacs --with-profile kimim
#+end_src

If you want use ~~/.emacs.kimim~ as the ~user-emacs-directory~, you need
to modify the value of variable ~kimim/path-kimim-emacs~ in
~early-init.el~.

#+begin_src emacs-lisp :tangle no
(defvar kimim/path-kimim-emacs "~/.emacs.kimim/")
#+end_src

** PATH and exec-path

Environment variable =PATH= is the the searching path of executables by
the shell running in Emacs while =exec-path= is the search path of Emacs
itself. So we should set both of them to almost the same paths.

As I have a Windows box in the office, and a Apple macOS at home, so I
need to specify these variables in different way.

#+begin_src emacs-lisp
(cond
 ((eq system-type 'cygwin)
  (setq kimim/path-root "/"))
 ((eq system-type 'darwin)
  (setq kimim/path-root "/")
  (add-to-list 'exec-path "/Library/TeX/texbin")
  (add-to-list 'exec-path (concat (getenv "HOME") "/Library/Python/2.7/bin")))
 ((eq system-type 'gnu/linux)
  (setq kimim/path-root "/")
  (add-to-list 'exec-path "/usr/local/texlive/2020/bin/x86_64-linux/")))

(add-to-list 'exec-path (concat kimim/path-root "bin"))
(add-to-list 'exec-path (concat kimim/path-root "usr/bin"))
(add-to-list 'exec-path (concat kimim/path-root "usr/local/bin"))
#+end_src

Then append exec-path to PATH:

#+begin_src emacs-lisp
(setenv "PATH"
        (concat
         (mapconcat #'identity exec-path path-separator)
         (getenv "PATH")))
#+end_src

For Windows/MSYS64, we need to modify =executable-find= to locate shell
scripts:

#+begin_src emacs-lisp
(defun executable-find (command &optional remote)
  "Search for COMMAND in `exec-path' and return the absolute file name.
Return nil if COMMAND is not found anywhere in `exec-path'.  If
REMOTE is non-nil, search on the remote host indicated by
`default-directory' instead."
  (if (and remote (file-remote-p default-directory))
      (let ((res (locate-file
                  command
                  (mapcar
                   (lambda (x) (concat (file-remote-p default-directory) x))
                   (exec-path))
                  exec-suffixes 'file-executable-p)))
        (when (stringp res) (file-local-name res)))
    ;; Use 1 rather than file-executable-p to better match the
    ;; behavior of call-process.
    (let ((default-directory (file-name-quote default-directory 'top)))
      (locate-file command exec-path exec-suffixes))))
#+end_src

** Language

I prefer to use English/UTF-8 as default language environment.

#+begin_src emacs-lisp
(setenv "LANG" "en_US.UTF-8")
(setenv "LC_ALL" "en_US.UTF-8")
;; remove svn log LC_TYPE not defined warning.
(setenv "LC_CTYPE" "en_US.UTF-8")
(setenv "LC_TIME" "en_US.UTF-8")
(set-locale-environment "en_US.UTF-8")
(set-language-environment 'English)
(prefer-coding-system 'utf-8)
(set-buffer-file-coding-system 'utf-8-unix)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(set-file-name-coding-system 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-clipboard-coding-system 'utf-8)
(cond
 ((member system-type '(windows-nt cygwin))
  (set-clipboard-coding-system 'utf-16le)))
#+end_src

** global key map

Define new command prefix for keys such as "C-x m f", "C-x m v".

#+begin_src emacs-lisp
(define-prefix-command 'ctl-x-m-map)
(global-set-key "\C-xm" 'ctl-x-m-map)
#+end_src

* Package

=package= [fn:8] is the modern =elisp= package management system, which
let you easily download and install packages that implement additional
features. Each package is a separate Emacs Lisp program, sometimes
including other components such as an Info manual.

All the extensions used in this file are installed and managed by
=package=.

Here I use =use-package= to defer the package loading and even
installation, When you use the =:commands= keyword, it creates autoloads
for those commands and defers loading of the module until they are
used.

When I want to upgrade elpa packages, I just call ~list-packages~, and
let emacs refresh the package lists, then I just need to press =U= to
upgrade all new packages.

If some weird things happen, for example ~compat-current-version~ error,
a ~byte-force-recompile~ in =~/.emacs.d= could work.

#+begin_src emacs-lisp
;; temporary disable signature check
(setq package-check-signature nil)
(setq package-user-dir "~/.emacs.d/elpa")
(setq package-archives
      '(;;("elpa" . "https://elpa.gnu.org/packages/")
        ;;("melpa" . "https://melpa.org/packages/")
        ("gnu" . "http://mirrors.tuna.tsinghua.edu.cn/elpa/gnu/")
        ("melpa" . "http://mirrors.tuna.tsinghua.edu.cn/elpa/melpa/")))
(mapc
 (lambda (package)
   (unless (package-installed-p package)
     (progn (message "installing %s" package)
            (package-refresh-contents)
            (package-install package))))
 '(use-package diminish bind-key))

(require 'use-package)
(require 'diminish)
(require 'bind-key)
;; install package if missing
(setq use-package-always-ensure t)
(setq use-package-always-defer t)
(setq use-package-verbose t)
#+end_src

* Look & Feel
** Menu Bar and Tool Bar

Don't display menu-bar, tool-bar, tooltip and scroll-bar. Because
sometimes, they may catch your attention. But you cannot hide menu bar
in macOS desktop environment, because the system preserves menu bar
for every applications.

#+begin_src emacs-lisp
(defun kimim/menu-and-bar ()
  (cond
   (window-system
    ;; Enable copy and paste in Win32
    (setq select-enable-clipboard t)
    (menu-bar-mode 0)
    (tool-bar-mode -1)
    (tooltip-mode -1)
    (scroll-bar-mode -1))
   ((eq window-system nil)
    (menu-bar-mode 0))))

(kimim/menu-and-bar)
#+end_src

** Font and Frame Size

Set default font and frame size for both window system. You should
=set-default-font= first, otherwise, the frame height and width will be
calculated with original default font height and width:
=frame-char-height= and =frame-char-width=.

#+begin_src emacs-lisp
(use-package cnfonts
  :bind (("C-+" . cnfonts-increase-fontsize)
         ("C--" . cnfonts-decrease-fontsize)
         ("C-=" . cnfonts-increase-fontsize)
         ("C-0" . cnfonts-reset-fontsize)))
#+end_src

#+begin_src emacs-lisp
(defun kimim/frame-and-font ()
  (interactive)
  (when window-system
    (require 'cnfonts)
    (cnfonts-enable)
    (cnfonts-set-font)
    ;; top, left ... must be integer
    (let ((width (display-pixel-width))
          (height (display-pixel-height))
          (frame (selected-frame)))
      (set-frame-position frame (/ width 10) (/ height 10))
      (set-frame-height frame (/ (* 4 height)
                                 (* 5 (frame-char-height))))
      (set-frame-width frame (/ (* 4 width)
                                (* 5 (frame-char-width))))
      (add-to-list 'default-frame-alist
                   (cons 'top  (/ height 10)))
      (add-to-list 'default-frame-alist
                   (cons 'left (/ width 10)))
      (add-to-list 'default-frame-alist
                   (cons 'height (/ (* 4 height)
                                    (* 5 (frame-char-height)))))
      (add-to-list 'default-frame-alist
                   (cons 'width (/ (* 4 width)
                                   (* 5 (frame-char-width))))))))

(kimim/frame-and-font)
#+end_src

** Frame Title

Customize the frame title to display buffer file name.

#+begin_src emacs-lisp
(setq frame-title-format
      '((:eval (buffer-name))))
#+end_src

** Mode Line

Display date and time, but do not display system load.

#+begin_src emacs-lisp
(use-package time
  :ensure nil
  :defer 0
  :custom
  (display-time-24hr-format t)
  (display-time-day-and-date t)
  (display-time-interval 10)
  (display-time-default-load-average nil)
  :config
  (display-time-mode t))
#+end_src

Show (line, column) numbers in mode line:

#+begin_src emacs-lisp
(use-package simple
  :ensure nil
  :defer 3
  :bind
  ;; cycling from one space, zero space and original space
  ("M-SPC" . cycle-spacing)
  :custom
  ;; put pastebin content to kill ring before kill others
  (save-interprogram-paste-before-kill t)
  :config
  (line-number-mode 1)
  (column-number-mode 1)
  (toggle-word-wrap -1))
#+end_src

** Celestial modeline

#+begin_src emacs-lisp
(use-package celestial-mode-line
  :disabled
  :defer 1
  :config
  (setq global-mode-string '("" celestial-mode-line-string "  " display-time-string ""))
  (celestial-mode-line-start-timer))
#+end_src

** Color Theme

Use =rainbow-mode= to edit colorful color string and symbol. In the
beginning, I add ~:hook prog-mode~, that means to enable ~rainbow-mode~
for all programming mode, but later, I find that ~#def~ part of ~#define~
in C is changed to gray color. Then I remove the this hook. So I will
turn on ~rainbow-mode~ manually, if I want to see the color.

#+begin_src emacs-lisp
(use-package rainbow-mode
  :diminish rainbow-mode)
#+end_src

Rainbow-delimiters is a "rainbow parentheses"-like mode which highlights
parentheses, brackets, and braces according to their depth.

#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :diminish rainbow-delimiters
  :hook (prog-mode . rainbow-delimiters-mode))
#+end_src

Toggle Font-Lock mode in all buffers.

#+begin_src emacs-lisp
(use-package font-lock
  :ensure nil
  :custom ((font-lock-maximum-decoration t)
           (font-lock-global-modes '(not shell-mode text-mode))
           (font-lock-verbose t))
  :config
  (global-font-lock-mode 1))
#+end_src

Use kimim-light as default theme.

#+begin_src emacs-lisp
(use-package custom
  :ensure nil
  :custom (;; do not warning when load new theme
           (custom-safe-themes t)
           ;; add my theme files to custom theme directory
           (custom-theme-directory "~/kimim-emacs/site-lisp/")))
#+end_src

** Image

Always set white background color for transparent images to display
clearly in dark color theme.

#+begin_src emacs-lisp
(use-package image
  :ensure nil
  :config
  (setq auto-mode-alist
        (append '(("\\.svg\\'" . image-mode))
                auto-mode-alist))
  (defun create-image-advice (im)
    (nconc im (list :background "#f8f8f8")))
  (advice-add 'create-image :filter-return #'create-image-advice))
#+end_src

** posframe

#+begin_src emacs-lisp
(use-package posframe)
#+end_src

** Other Visual Element

#+begin_src emacs-lisp
(setq inhibit-startup-message t)
(setq initial-scratch-message nil)
(setq visible-bell t)
(setq ring-bell-function #'ignore)
(fset 'yes-or-no-p 'y-or-n-p)
(show-paren-mode 1)
(setq blink-cursor-blinks 3)
(blink-cursor-mode 1)
(tooltip-mode -1)
;; mark highlight in other windows also
(setq highlight-nonselected-windows nil)
#+end_src

#+begin_src emacs-lisp
(use-package hi-lock
  :custom (hi-lock-auto-select-face t))
#+end_src

* Help & References
** Info

#+begin_src emacs-lisp
(use-package info
  :commands (info)
  :config
  (add-to-list 'Info-additional-directory-list
               (concat kimim/path-root "usr/share/info"))
  (add-to-list 'Info-additional-directory-list
               (concat kimim/path-root "usr/local/share/info"))
  ;; additional info, collected from internet
  (add-to-list 'Info-additional-directory-list
               "~/info"))
#+end_src

** Youdao dictionary

Search dictionary with Ctrl+F3 by youdao dictionary.

#+begin_src emacs-lisp
(use-package youdao-dictionary
  :bind (
         ("C-x m 1" . youdao-dictionary-search-at-point-posframe)
         ("C-x m 2" . youdao-dictionary-search)
         :map youdao-dictionary-mode-map
         ("C-y" . youdao-dictionary-search-yanked)
         ("<mouse-3>" . youdao-dictionary-def-copied)
         ("f" . youdao-dictionary-search-from-input))
  :config
  ;; redefine youdao-format-result
  (defun youdao-dictionary--format-result (json)
    "Format result in JSON."
    (let* ((query        (assoc-default 'query       json)) ; string
           (translation  (assoc-default 'translation json)) ; array
           (_errorCode    (assoc-default 'errorCode   json)) ; number
           (web          (assoc-default 'web         json)) ; array
           (basic        (assoc-default 'basic       json)) ; alist
           ;; construct data for display
           (phonetic (assoc-default 'phonetic basic))
           (translation-str (mapconcat
                             (lambda (trans) (concat "- " trans))
                             translation "\n"))
           (basic-explains-str (mapconcat
                                (lambda (explain) (concat "- " explain))
                                (assoc-default 'explains basic) "\n"))
           (web-str (mapconcat
                     (lambda (k-v)
                       (format "- %s :: %s"
                               (assoc-default 'key k-v)
                               (mapconcat 'identity (assoc-default 'value k-v) "; ")))
                     web "\n")))
      (let ((result-str
             (if basic
                 (format "%s [%s]\n\nBasic Explains\n%s\n\nWeb References\n%s\n"
                         query phonetic basic-explains-str web-str)
               (format "%s\n\nTranslation\n%s\n"
                       query translation-str))))
        (kill-new
         (mapconcat
          (lambda (x)
            (concat x "\n"))
          (cdr (split-string result-str "\n"))))
        (kill-new (format "%s [%s]" query phonetic))
        result-str)))

  (defun youdao-dictionary-def-copied ()
    (interactive)
    (youdao-dictionary-search (gui-get-selection)))
    (defun youdao-dictionary-search-yanked ()
    (interactive)
    (youdao-dictionary-search
     (if (gui-get-selection)
         (gui-get-selection)
       (car kill-ring)))))
#+end_src

** fanyi.el

A translator extension written by [[https://condy0919.github.io][Youmu]].

#+begin_src emacs-lisp
(use-package fanyi
  :ensure t
  :custom
  (fanyi-providers '(;; 海词
                     fanyi-haici-provider
                     ;; 有道同义词词典
                     fanyi-youdao-thesaurus-provider
                     ;; Etymonline
                     fanyi-etymon-provider
                     ;; Longman
                     fanyi-longman-provider))
  :bind (:map fanyi-mode-map
              ("n" . outline-next-visible-heading)
              ("p" . outline-previous-visible-heading)))
#+end_src

* Editing
** Input Method

#+begin_src emacs-lisp
(use-package rime
  :bind ("C-;" . toggle-input-method)
  :custom
  (default-input-method "rime")
  (rime-disable-predicates
   '(rime-predicate-after-alphabet-char-p))
  (rime-show-candidate 'posframe)
  (rime-posframe-properties
   (list :font "YaheiInconsolata-18"
         :internal-border-width 14))
  :config
  (when (eq (window-system) 'mac)
    (setq rime-librime-root "~/.emacs.d/librime/dist")))
#+end_src

** General

#+begin_src emacs-lisp
(use-package autorevert
  :ensure nil
  :diminish auto-revert-mode)
#+end_src

#+begin_src emacs-lisp
(setq inhibit-eol-conversion nil)
;; fill-column is a buffer-local variable
;; use setq-default to change it globally
;; (if window-system
;;     (setq-default fill-column
;;                   (min 70
;;                        (ceiling
;;                         (/ (x-display-pixel-width)
;;                            (frame-char-width)
;;                            2.3))))
;;   (setq-default fill-column 70))
(setq-default fill-column 70)
(use-package drag-stuff
  :diminish drag-stuff-mode
  :config
  (drag-stuff-global-mode 1))
(delete-selection-mode 1)
(setq kill-ring-max 200)
(setq kill-whole-line t)
(setq require-final-newline t)
(setq-default tab-width 4)
(setq tab-stop-list
      (number-sequence 4 120 4))
;; stretch to tab width when on tab
(setq x-stretch-cursor t)
(setq track-eol t)
(setq backup-directory-alist '(("." . "~/temp")))
(setq version-control t)
(setq kept-old-versions 10)
(setq kept-new-versions 20)
(setq delete-old-versions t)
(setq backup-by-copying t)

(setq auto-save-interval 50)
(setq auto-save-timeout 60)
(setq auto-save-default nil)
(setq auto-save-list-file-prefix "~/temp/auto-saves-")
(setq auto-save-file-name-transforms `((".*"  , "~/temp/")))
(setq create-lockfiles nil)
(use-package time-stamp
  :config
  (setq time-stamp-active t)
  (setq time-stamp-warn-inactive t)
  (setq time-stamp-format "%:y-%02m-%02d %3a %02H:%02M:%02S Kimi MA")
  (add-hook 'write-file-functions 'time-stamp))

(defun kimim/save-buffer-advice (orig-fun &rest arg)
  (when (not (memq major-mode '(org-mode markdown-mode text-mode)))
      (delete-trailing-whitespace))
  (apply orig-fun arg))

(advice-add 'save-buffer :around #'kimim/save-buffer-advice)

(diminish 'visual-line-mode)
(add-hook 'text-mode-hook
          (lambda ()
            (when (derived-mode-p 'org-mode 'markdown-mode
                                  'text-mode 'info-mode)
              (visual-line-mode)
              (setq line-spacing 0.4))))
(setq-default indent-tabs-mode nil)

(setq uniquify-buffer-name-style 'forward)
(setq suggest-key-bindings 5)

(add-to-list 'auto-mode-alist '("\\.css\\'" . css-mode))
(add-to-list 'auto-mode-alist '("\\.S\\'" . asm-mode))
(add-to-list 'auto-mode-alist '("\\.svg\\'" . html-mode))
(add-to-list 'auto-mode-alist '("\\.pas\\'" . delphi-mode))

(require 'saveplace)
(setq-default save-place t)
(setq save-place-file (expand-file-name "saveplace" "~"))
#+end_src

** multi cursors

You can mark a region, and ~C-S-c C-S-c~ to start edit every line in this
region. That's amazing.

#+begin_src emacs-lisp
(use-package multiple-cursors
  :bind
  ("C-S-c C-S-c" . mc/edit-lines)
  ("C->" . mc/mark-next-like-this)
  ("C-<" . mc/mark-previous-like-this)
  ("C-c C-<" . mc/mark-all-like-this)
  ("C-c C->" . mc/mark-all-dwim))
#+end_src

** visual-fill-column

Markdown file is not fill-column'ed, so let emacs visually
fill-column.

#+begin_src emacs-lisp
;; (use-package visual-fill-column
;;   :hook (markdown-mode . visual-fill-column-mode))
#+end_src

** Highlight

Highlight current line in window systems, but disable this in
terminal. Because the line highlight will cause the terminal blinking.

#+begin_src emacs-lisp
(use-package hl-line
  :if window-system
  :config
  (global-hl-line-mode -1))
#+end_src

** ispell

#+begin_src emacs-lisp
(use-package ispell
  :custom (ispell-program-name "hunspell")
  :config
  (cond ((eq system-type 'windows-nt)
         (setq ispell-alternate-dictionary "~/.emacs.d/dict/words.txt"))))
#+end_src

** flyspell

Check spell on the fly.

#+begin_src emacs-lisp
(use-package flyspell
  :diminish flyspell-mode
  :hook (;;(prog-mode . flyspell-prog-mode)
         (org-mode . flyspell-mode)))
#+end_src

** olivetti

#+begin_src emacs-lisp
(use-package olivetti
  :diminish olivetti-mode
  :custom
  (olivetti-body-width (+ fill-column 10))
  :hook ((text-mode elfeed-show-mode eww-mode Info-mode woman-mode)
	     . olivetti-mode)
  :config
  (add-hook 'cnfonts-set-font-finish-hook
	        (lambda (args)
		      (let ((frame-width (* (+ fill-column 10)
				                    (frame-char-width))))
		        (setq org-image-actual-width frame-width)
		        (org-redisplay-inline-images)))))
#+end_src

** Chinese word segmentation

#+begin_src emacs-lisp
(use-package cns
  :after text-mode
  :load-path (lambda ()
               (concat kimim/path-kimim-emacs
                "site-lisp/cns"))
  :hook (text-mode . cns-mode)
  :bind ((:map cns-mode-map)
         ("M-h" . cns-backward-kill-word))
  :config
  (setq cns-prog
        (concat kimim/path-kimim-emacs
                "site-lisp/cns/cnws.exe"))
  (setq cns-dict-directory
        (concat kimim/path-kimim-emacs
                "site-lisp/cns/cppjieba/dict"))
  (setq cns-recent-segmentation-limit 20) ; default is 10
  (setq cns-debug nil))
#+end_src

* Controlling
** Window and Frame

By enabling ~winner-mode~, you can restore to previous window configuration by
typing ~C-c <left>~.

#+begin_src emacs-lisp
(use-package winner
  ;; restore windows configuration, built-in package
  :commands winner-mode
  :config
  (winner-mode t))
#+end_src

When type ~C-x m w~ it will create a new frame with the default frame
configuration.

Before an emacsclient first connect to daemon, the daemon is working
in terminal mode. Thus ~(display-graphic-p)~ will return ~nil~. So I add
~raise-frame~ in ~after-make-frame-functions~ to force emacs to bring the
new frame to the front and apply the gui related settings.

#+begin_src emacs-lisp
(use-package frame
  :ensure nil
  :defer 1
  :bind ("C-x m w" . make-frame)
  :config
  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (select-frame frame)
              (kimim/menu-and-bar)
              (kimim/frame-and-font)
              (raise-frame frame))))
#+end_src

preserve the point in screen during scrolling looks nice(see [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Scrolling.html][scrolling]]). scroll
slowly with touchpad, thus we adjust the scroll amount.

#+begin_src emacs-lisp
(setq scroll-preserve-screen-position t)
(setq mouse-wheel-scroll-amount '(0.01))
#+end_src

** Command

Display key candidates when you typed part key prefix with ~which-key-mode~.

#+begin_src emacs-lisp
;; https://github.com/justbur/emacs-which-key
(use-package which-key
  :defer 3
  :diminish which-key-mode
  :custom (which-key-popup-type 'side-window)
  :config
  (which-key-mode 1))
#+end_src

List recent used commands with ~smex~:

#+begin_src emacs-lisp

;; smex will list the recent function on top of the cmd list
(use-package smex
  :commands (smex)
  :config
  (smex-initialize))
#+end_src

** Key Frequency

We will use =keyfreq= to record the frequency of the key typing, and get a
frequency report by =M-x keyfreq-show=.

#+begin_src emacs-lisp
(use-package keyfreq
  :custom (keyfreq-file "~/.emacs.d/emacs.keyfreq")
  :config
  (keyfreq-mode +1)
  (keyfreq-autosave-mode +1))
#+end_src

** eshell

#+begin_src emacs-lisp
(use-package company-shell
  :commands company-shell)

(use-package eshell
  :config
  (add-hook 'eshell-mode-hook
            (lambda ()
              (setq company-backends
                    '((company-shell company-files)
                      company-capf company-yasnippet company-dabbrev company-ebdb company-ispell
                      (company-dabbrev-code company-gtags company-etags company-keywords))))))
#+end_src

** Navigation

#+begin_src emacs-lisp
(use-package bookmark
  :custom
  (bookmark-default-file "~/.emacs.d/emacs.bmk")
  (bookmark-save-flag 1)
  (bookmark-fontify nil)
  :config
  (add-hook 'bookmark-after-jump-hook
            (lambda ()
              (recenter 'top))))
#+end_src

~bm~ is used to temporally toggle buffer local bookmarks with ~C-x m t~,
then you can view all the local temporally bookmarks with ~C-x m s~.

#+begin_src emacs-lisp
(use-package bm
  :bind (("C-x m t" . bm-toggle)
         ("C-x m s" . bm-show-all)
         ("C-x m <left>" . bm-previous)
         ("C-x m <right>" . bm-next)))
#+end_src

You can jump to any character by triggering ~ace-jump-mode~ (~C-x m c~),
and jump to any window by triggering ~ace-window~ (~C-x m w~).

#+begin_src emacs-lisp
(use-package ace-window
  :bind
  (("C-x o" . ace-window)
   ("M-o" . other-window)
   ("C-x m w" . ace-swap-window)
   ("C-x m x" . ace-delete-window))
  :custom
  (aw-keys '(?a ?s ?d ?f ?g ?h ?j ?k ?l)))
#+end_src

* Search and Finding
** swiper replaces isearch

#+begin_src emacs-lisp
(use-package swiper
  :custom
  (swiper-action-recenter t)
  :bind
  ("C-s" . swiper)
  ("M-s ." . swiper-thing-at-point))
#+end_src

** imenu & imenu-anywhere

=imenu= is used to navigate the function definitions in current buffer.

#+begin_src emacs-lisp
(use-package imenu
  :functions kimim/imenu-default-goto-function-advice
  :bind (("C-x i" . imenu)
         ("C-c i" . lsp-ui-imenu))
  :config
  (require 'lsp-ui)
  (advice-add 'imenu-default-goto-function
              :around
              #'kimim/imenu-default-goto-function-advice))

(use-package imenu-anywhere
  :bind ("C-x m i" . imenu-anywhere))
#+end_src

** ripgrep: a fast command line search tool
#+begin_src emacs-lisp
(use-package ripgrep
  :bind ("C-x g" . ripgrep-regexp))
#+end_src

** search from web

#+begin_src emacs-lisp
(use-package eww
  :custom
  (eww-search-prefix "https://cn.bing.com/search?q="))
#+end_src

** recentf

#+begin_src emacs-lisp
(use-package recentf
  :config
  (recentf-mode))
#+end_src
** avy
#+begin_src emacs-lisp
(use-package avy
  :bind ("C-x m g" . avy-goto-word-or-subword-1))
#+end_src

* File Management
** delete files

To avoid accidentally delete files, let emacs move the deleted file to trash.

#+begin_src emacs-lisp
(setq delete-by-moving-to-trash t)
#+end_src

** dired

#+begin_src emacs-lisp
(use-package dired
  :ensure nil
  :defines (dired-omit-localp
            dired-omit-files)
  :functions (dired-omit-mode
              dired-dwim-target-directory
              kimim/drawio-to)
  :custom
  (dired-listing-switches "-AGhlgov")
  (dired-recursive-copies t)
  (dired-recursive-deletes t)
  (ls-lisp-dirs-first t)
  (dired-create-destination-dirs 'ask)
  (dired-dwim-target t)
  :bind
  (("C-x C-j" . dired-jump)
   :map dired-mode-map
   ("<left>" . dired-up-directory)
   ("<right>" . dired-find-file)
   ("b" . dired-up-directory)
   ("e" . dired-efap)
   ("o" . kimim/open-external)
   ("M-n" . dired-narrow)
   ("M-c" . compose-attach-marked-files)
   ("C-q" . kill-dired-buffers)
   ("<tab>" . kimim/dired-other-window))
  :config
  (require 'dired-filter)
  (require 'dired-recent)
  (require 'dired-x)
  (require 'dired-efap)
  (require 'kimim) ;; for kimim/open-external
  (add-hook 'dired-mode-hook
            (lambda ()
              (turn-on-gnus-dired-mode)
              ;; Set dired-x buffer-local variables here.  For example:
              ;;(dired-omit-mode 1)
              (dired-filter-mode 1)
              (hl-line-mode 1)
              (setq dired-omit-localp t)
              (setq dired-omit-files
                    (concat "_minted[.]*\\|desktop.ini"
                            "\\|NTUSER\\|ntuser"
                            "\\|Cookies\\|AppData"
                            "\\|Contacts\\|Links"
                            "\\|Intel\\|NetHood"
                            "\\|PrintHood\\|Recent"
                            "\\|Start\\|SendTo"
                            "\\|^\\.DS_Store"
                            "\\|qms-bmh"))))
  (if (eq system-type 'darwin)
      (setq dired-listing-switches "-Avhlgo"))

  (defun compose-attach-marked-files ()
    "Compose mail and attach all the marked files from a dired buffer."
    (interactive)
    (let ((files (dired-get-marked-files))
          (file-names (dired-copy-filename-as-kill)))
      (compose-mail nil (concat "Attachments: " file-names) nil t)
      (dolist (file files)
        (if (file-regular-p file)
            (mml-attach-file file
                             (mm-default-file-type file)
                             nil "attachment")
          (message "skipping non-regular file %s" file)))))

  (defadvice dired-next-line (after dired-next-line-advice (arg) activate)
    "Move down lines then position at filename, advice"
    (interactive "p")
    (if (eobp)
        (progn
          (goto-char (point-min))
          (forward-line 1)
          (dired-move-to-filename))))

  (defadvice dired-previous-line (before dired-previous-line-advice (arg) activate)
    "Move up lines then position at filename, advice"
    (interactive "p")
    (if (= 2 (line-number-at-pos))
        (goto-char (point-max))))

  (defun kimim/dired-other-window ()
    (interactive)
    (let ((other-dired-buffer (dired-dwim-target-directory)))
      (if other-dired-buffer
          (dired-other-window other-dired-buffer)
        (dired-jump-other-window))))

  (defun kimim/dired-get-org-link ()
    "get a link from dired for org"
    (interactive)
    (let ((filename (dired-get-filename)))
      (kill-new (concat
                 "[["
                 filename
                 "]["
                 (file-name-nondirectory filename)
                 "]]"))))

  (defun kimim/open-with-inkscape ()
    (interactive)
    (let* ((filename (dired-get-filename)))
      (w32-shell-execute
       "open" (concat (getenv "MSYS64_PATH") "\\kimikit\\inkscape\\bin\\inkscape.exe")
       filename)))

  (defun kimim/pdf2svg ()
    (interactive)
    (let* ((filename (dired-get-filename))
           (exportname (replace-regexp-in-string ".pdf$" ".svg" filename))
           (counter 10))
      (w32-shell-execute
       "open" (concat (getenv "MSYS64_PATH") "\\kimikit\\inkscape\\bin\\inkscape.exe")
       (format "--export-filename=\"%s\" \"%s\""
               exportname filename))
      (while (and (not (file-exists-p exportname))
                  (> counter 0))                    ; true-or-false-test
        (sleep-for 1)
        (setq counter (1- counter)))
      (dired-revert)))

  (defun kimim/drawio-to (ext)
    (let* ((filename (dired-get-filename))
           (exportname (replace-regexp-in-string "drawio$" ext filename))
           (counter 10))
      (w32-shell-execute
       "open" (concat (getenv "MSYS64_PATH")
                      "\\kimikit\\draw.io\\draw.io.exe")
       (format "--crop -b 5 -x \"%s\" -o \"%s\""
               filename exportname))
      (while (and (not (file-exists-p exportname))
                  (> counter 0))                    ; true-or-false-test
        (sleep-for 1)
        (setq counter (1- counter)))
      (dired-revert)))

  (defun kimim/drawio2png ()
    (interactive)
    (kimim/drawio-to "png"))

  (defun kimim/drawio2svg ()
    (interactive)
    (kimim/drawio-to "svg"))

  (defun kimim/drawio2pdf ()
    (interactive)
    (kimim/drawio-to "pdf")))
#+end_src

** dired-recent

Keep a list of recently visited directories. Then we can quickly revisit them.

#+begin_src emacs-lisp
(use-package dired-recent
  :config
  (dired-recent-mode 1))
#+end_src

** dired-efap

dired-efap, Edit file at point, can be used to rename file name at the point:

#+begin_src emacs-lisp
(use-package dired-efap
  :commands dired-efap)
#+end_src

** dired-narrow

~M-n~ will prompt for strings to narrow the files in current dired buffer.

#+begin_src emacs-lisp
(use-package dired-narrow
  :commands dired-narrow)
#+end_src

** dired-filter

#+begin_src emacs-lisp
(use-package dired-filter
  :diminish dired-filter-mode)
#+end_src

** ibuffer

#+begin_src emacs-lisp
(use-package ibuffer
  :bind (("C-x C-b" . ibuffer-other-window)
         :map ibuffer-mode-map
         ("<right>" . ibuffer-visit-buffer))
  :custom
  (ibuffer-formats
   '((mark modified read-only " "
           (name 32 32 :left :elide)
           " "
           (size-h 9 -1 :right)
           " "
           (mode 14 14 :left :elide)
           " "
           filename-and-process)))
  :config
  ;; Use human readable Size column instead of original one
  (define-ibuffer-column size-h
    (:name "Size" :inline t)
    (cond
     ((> (buffer-size) 1000000) (format "%7.1fM" (/ (buffer-size) 1000000.0)))
     ((> (buffer-size) 100000) (format "%7.0fk" (/ (buffer-size) 1000.0)))
     ((> (buffer-size) 1000) (format "%7.1fk" (/ (buffer-size) 1000.0)))
     (t (format "%8d" (buffer-size))))))
#+end_src

* Completion
** marginalia
Rich annotations in the minibuffer.

#+begin_src emacs-lisp
(use-package marginalia
  :init
  (marginalia-mode))
#+end_src

** consult
#+begin_src emacs-lisp
(use-package consult
  :defines (xref-show-xrefs-function
            xref-show-definitions-function)
  :bind (;; C-c bindings (mode-specific-map)
         ("C-c h" . consult-history)
         ("C-c m" . consult-mode-command)
         ("C-c k" . consult-kmacro)
         ;; C-x bindings (ctl-x-map)
         ("C-x M-:" . consult-complex-command)     ;; orig. repeat-complex-command
         ("C-x b" . consult-buffer)                ;; orig. switch-to-buffer
         ("C-x 4 b" . consult-buffer-other-window) ;; orig. switch-to-buffer-other-window
         ("C-x 5 b" . consult-buffer-other-frame)  ;; orig. switch-to-buffer-other-frame
         ("C-x M-b" . consult-buffer-other-window)
         ("C-x m j" . consult-bookmark)            ;; orig. bookmark-jump
         ("C-x m v" . find-variable)
         ("C-x m f" . find-function)
         ("C-x p b" . consult-project-buffer)      ;; orig. project-switch-to-buffer
         ;; Custom M-# bindings for fast register access
         ("M-#" . consult-register-load)
         ("M-'" . consult-register-store)          ;; orig. abbrev-prefix-mark (unrelated)
         ("C-M-#" . consult-register)
         ;; Other custom bindings
         ("M-y" . consult-yank-pop)                ;; orig. yank-pop
         ("<help> a" . consult-apropos)            ;; orig. apropos-command
         ;; M-g bindings (goto-map)
         ("M-g e" . consult-compile-error)
         ("M-g f" . consult-flymake)               ;; Alternative: consult-flycheck
         ("M-g g" . consult-goto-line)             ;; orig. goto-line
         ("M-g M-g" . consult-goto-line)           ;; orig. goto-line
         ("M-g o" . consult-outline)               ;; Alternative: consult-org-heading
         ("M-g m" . consult-mark)
         ("M-g k" . consult-global-mark)
         ("M-g i" . consult-imenu)
         ("M-g I" . consult-imenu-multi)
         ;; M-s bindings (search-map)
         ("M-s d" . consult-find)
         ("M-s D" . consult-locate)
         ("M-s g" . consult-grep)
         ("M-s G" . consult-git-grep)
         ("M-s r" . consult-ripgrep)
         ("M-s p" . kimim/consult-ripgrep-current)
         ("M-s l" . consult-line)
         ("M-s L" . consult-line-multi)
         ("M-s m" . consult-multi-occur)
         ("M-s k" . consult-keep-lines)
         ("M-s u" . consult-focus-lines)
         ;; Isearch integration
         ("M-s e" . consult-isearch-history)
         :map isearch-mode-map
         ("M-e" . consult-isearch-history)         ;; orig. isearch-edit-string
         ("M-s e" . consult-isearch-history)       ;; orig. isearch-edit-string
         ("M-s l" . consult-line)                  ;; needed by consult-line to detect isearch
         ("M-s L" . consult-line-multi)
         :map minibuffer-local-map
         ("M-s" . consult-history)                 ;; orig. next-matching-history-element
         ("M-r" . consult-history))           ;; needed by consult-line to detect isearch

  ;; Enable automatic preview at point in the *Completions* buffer. This is
  ;; relevant when you use the default completion UI.
  ;;:hook (completion-list-mode . consult-preview-at-point-mode)

  ;; The :init configuration is always executed (Not lazy)
  :init
  (global-set-key [remap repeat-complex-command] #'consult-complex-command)
  (use-package recentf)
  ;; Optionally configure the register formatting. This improves the register
  ;; preview for `consult-register', `consult-register-load',
  ;; `consult-register-store' and the Emacs built-ins.
  (setq register-preview-delay 0.5
        register-preview-function #'consult-register-format)

  ;; Optionally tweak the register preview window.
  ;; This adds thin lines, sorting and hides the mode line of the window.
  (advice-add #'register-preview :override #'consult-register-window)

  ;; Use Consult to select xref locations with preview
  (setq xref-show-xrefs-function #'consult-xref
        xref-show-definitions-function #'consult-xref)

  ;; Configure other variables and modes in the :config section,
  ;; after lazily loading the package.
  :config

  (defun kimim/consult-ripgrep-current ()
    (interactive)
    (consult-ripgrep "."))

  ;; Optionally configure preview. The default value
  ;; is 'any, such that any key triggers the preview.
  ;; (setq consult-preview-key '(:debounce 1.5 any))
  ;; (setq consult-preview-key 'any)
  ;; (setq consult-preview-key "M-.")
  ;; (setq consult-preview-key (list "<S-down>") (kbd "<S-up>"))
  ;; For some commands and buffer sources it is useful to configure the
  ;; :preview-key on a per-command basis using the `consult-customize' macro.
  (consult-customize
   consult-theme consult--source-buffer
   :preview-key '(:debounce 5 any)
   consult-ripgrep consult-git-grep consult-grep
   consult-bookmark consult-recent-file consult-xref
   consult--source-bookmark consult--source-recent-file
   consult--source-project-recent-file
   :preview-key "M-.")

  ;; Optionally configure the narrowing key.
  ;; Both < and C-+ work reasonably well.
  (setq consult-narrow-key "<")
)

;; Enable vertico
(use-package vertico
  :init
  (vertico-mode)

  ;; Different scroll margin
  ;; (setq vertico-scroll-margin 0)

  ;; Show more candidates
  ;; (setq vertico-count 20)

  ;; Grow and shrink the Vertico minibuffer
  ;; (setq vertico-resize t)

  ;; Optionally enable cycling for `vertico-next' and `vertico-previous'.
  ;; (setq vertico-cycle t)
  )

(use-package orderless
  :init
  (setq completion-styles '(orderless)
        completion-category-defaults nil
        completion-category-overrides '((file (styles partial-completion)))))

;; Persist history over Emacs restarts. Vertico sorts by history position.
(use-package savehist
  :init
  (savehist-mode))

;; A few more useful configurations...
(use-package emacs
  :init
  ;; Add prompt indicator to `completing-read-multiple'.
  ;; Alternatively try `consult-completing-read-multiple'.
  (defun crm-indicator (args)
    (cons (concat "[CRM] " (car args)) (cdr args)))
  (advice-add #'completing-read-multiple :filter-args #'crm-indicator)

  ;; Do not allow the cursor in the minibuffer prompt
  (setq minibuffer-prompt-properties
        '(read-only t cursor-intangible t face minibuffer-prompt))
  (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)

  ;; Emacs 28: Hide commands in M-x which do not work in the current mode.
  ;; Vertico commands are hidden in normal buffers.
  ;; (setq read-extended-command-predicate
  ;;       #'command-completion-default-include-p)

  ;; Enable recursive minibuffers
  (setq enable-recursive-minibuffers t))
#+end_src

** abbrev

#+begin_src emacs-lisp
(diminish 'abbrev-mode)
#+end_src

** yasnippet

#+begin_src emacs-lisp
(use-package yasnippet
  :defer 10
  :diminish yas-minor-mode
  :defines warning-suppress-types
  :config
  (require 'warnings)
  (add-to-list
   'yas-snippet-dirs (concat kimim/path-sync-emacs "snippets"))
  (yas-global-mode 1)
  (setq warning-suppress-types '((yasnippet backquote-change))))
#+end_src

In order to remove following warning:

#+BEGIN_QUOTE
Warning (yasnippet): ‘xxx’ modified buffer in a backquote expression.
  To hide this warning, add (yasnippet backquote-change) to ‘warning-suppress-types’.
#+END_QUOTE

** company mode

#+begin_src emacs-lisp
(use-package company
  :defer 10
  :functions (company-abort)
  :bind (:map company-active-map
              ("C-n" . company--select-next-and-warn)
              ("C-p" . company--select-previous-and-warn)
              ("C-h" . delete-backward-char)
              ("C-d" . delete-forward-char)
              ("SPC" . (lambda ()
                         (interactive)
                         (company-abort)
                         (insert-char ?\x20))))
  :diminish company-mode
  :commands (global-company-mode)
  :custom
  (company-idle-delay 0)
  (company-minimum-prefix-length 1)
  :config
  (require 'company-posframe)
  (global-company-mode t)
  (setq company-backends
        '((company-yasnippet
           company-keywords
           company-capf
           company-files :separate)
          (company-dabbrev
           company-dabbrev-code
           company-ebdb
           company-ispell :with))))
#+end_src

** company-try-hard

If no candidates satisfies our needs, we can type ~C-\~ to get more
candidates from following backends from ~company-backends~.

#+begin_src emacs-lisp
(use-package company-try-hard
  :bind ("C-\\" . company-try-hard))
#+end_src

** company-posframe

This extension won't clutter the buffer contents.

#+begin_src emacs-lisp
(use-package company-posframe
  :diminish company-posframe-mode
  :config
  (company-posframe-mode 1))
#+end_src

** company-ebdb

#+begin_src emacs-lisp
(use-package company-ebdb)
#+end_src

** company statistics

Sort candidates using completion history.

#+begin_src emacs-lisp
(use-package company-statistics
  :config
  (company-statistics-mode 1))
#+end_src

* Programming General

** Project

~project-find-file~ (~C-x p f~) can find files of current project, indicated by git
or other version control information.

#+begin_src emacs-lisp :results none
(use-package project
  :bind (("C-x p r" . project-find-ripgrep-regexp)
         ("C-x p s" . kimim/magit-stage-file))
  :functions (magit-stage-1
              magit-with-toplevel
              magit-untracked-files
              magit-unstaged-files
              magit-file-relative-name
              project--read-regexp)
  :config
  (require 'magit)
  (defun project-find-ripgrep-regexp (regexp)
    "Find all matches for REGEXP in the current project's roots."
    (interactive (list (project--read-regexp)))
    (require 'ripgrep)
    (let* ((caller-dir default-directory)
           (pr (project-current t))
           (default-directory (project-root pr))
           (dir
            (if (not current-prefix-arg)
                default-directory
              (read-directory-name "Base directory: "
                                   caller-dir nil t))))
      (ripgrep-regexp regexp dir)))

  (defun kimim/magit-stage-file ()
    (interactive)
    (let* ((current (magit-file-relative-name))
           (choices (nconc (magit-unstaged-files)
                           (magit-untracked-files)))
           (default (car (member current choices))))
      (if default
          (magit-with-toplevel
            (magit-stage-1 nil (list default)))
        (message "Already staged")))))
#+end_src

** Compiling

#+begin_src emacs-lisp
(setq next-error-recenter 20)
(setq compilation-scroll-output t)
(bind-key "C-<f11>" 'compile)
#+end_src

** Version Control

Bind ~magit~ to ~C-x p m~ with the same prefix of ~project~, as they have
strong relationship.

#+begin_src emacs-lisp :results none
(use-package magit
  :bind (("C-x p m" . magit-status-here)
         (:map magit-revision-mode-map)
         ("C-<return>" . magit-diff-visit-file-other-window)
         ("<SPC>" . magit-diff-visit-worktree-file-other-window))
  :custom (magit-log-show-refname-after-summary t))
#+end_src

Following error will reported when using magit to commit changes:

#+BEGIN_QUOTE
server-ensure-safe-dir: The directory ‘~/.emacs.d/server’ is unsafe
#+END_QUOTE

The solution is to change the owner of =~/.emacs.d/server= [fn:9]

#+BEGIN_QUOTE
Click R-mouse on ~/.emacs.d/server and select “Properties” (last item in
menu). From Properties select the Tab “Security” and then select the button
“Advanced”. Then select the Tab “Owner” and change the owner from
=“Administrators (\Administrators)”= into =“ (\”=. Now the server code will accept
this directory as secure because you are the owner.
#+END_QUOTE

** Parenthesis

~smartparens-mode~[fn:10] is a general purpose mode for dealing with
parenthesis. We define some keys for it:

#+begin_src emacs-lisp
(use-package smartparens
  :bind (:map
         smartparens-mode-map
         ("C-<right>" . sp-forward-slurp-sexp)
         ("C-<left>" . sp-forward-barf-sexp)
         ("M-<right>" . sp-backward-barf-sexp)
         ("M-<left>" . sp-backward-slurp-sexp)
         ("M-<up>" . sp-splice-sexp-killing-backward)
         ("M-<down>" . sp-splice-sexp-killing-forward)
         ("C-k" . sp-kill-hybrid-sexp)
         ("M-k" . sp-kill-sexp)
         ("<backspace>" . sp-backward-delete-char)
         ("C-d" . sp-delete-char)
         ("C-M-<backspace>" . sp-backward-copy-sexp)
         ("C-M-w" . sp-copy-sexp))
  :functions (sp-local-pair)
  :hook (prog-mode . smartparens-mode)
  :diminish smartparens-mode
  :config
  (sp-with-modes '(c-mode c++-mode)
    (sp-local-pair "<" ">" :actions '(wrap autoskip navigate)))
  (sp-with-modes sp-lisp-modes
    ;; disable ', it's the quote character!
    (sp-local-pair "'" nil :actions nil)
    ;; disable ', it's the backquote character!
    (sp-local-pair "`" nil :actions nil)
    ;; also only use the pseudo-quote inside strings where it
    ;; serves as hyperlink.
    (sp-local-pair "`" "'" :when '(sp-in-string-p sp-in-comment-p))))
#+end_src

** Code folding

With ~yafolding-mode~, you can:
- toggle the code folding with ~yafolding-toggle-element~ (~C-<return>~)
- toggle global folding with ~yafolding-toggle-all~ (~C-M-<return>~)

#+begin_src emacs-lisp
(use-package yafolding
  :hook (prog-mode . yafolding-mode))
#+end_src

** static code analysis

#+begin_src emacs-lisp
(use-package flycheck
  :commands (global-flycheck-mode)
  :custom
  (flycheck-global-modes '(not org-mode)))
#+end_src

** lsp mode

~lsp-mode~ aims to provide IDE-like experience.

If you got error: =Symbol’s function definition is void: -compose=, make sure
that dash version higher than 2.18 is installed[fn:11].

#+begin_src emacs-lisp
(use-package dash
  :ensure t)
#+end_src

#+begin_src emacs-lisp
(use-package goto-addr
  :config
  ;;(setq goto-address-uri-schemes-ignored '("mailto:" "data:" "jar:"))
  (setq goto-address-uri-schemes
        (cons "C:" (seq-reduce (lambda (accum elt) (delete elt accum))
                        goto-address-uri-schemes-ignored
                        (copy-sequence thing-at-point-uri-schemes))))
  (setq goto-address-url-regexp
        (concat "\\<"
                (regexp-opt goto-address-uri-schemes t)
                thing-at-point-url-path-regexp)))
#+end_src

#+begin_src emacs-lisp
(use-package treemacs)
#+end_src

~lsp-mode~ will add ~company-capf~ in front for ~company-backends~, then it
will prevent my global ~company-backends~ settings. You can customize
~lsp-completion-provider~ to ~:none~ prevent this.

#+begin_src emacs-lisp
(use-package lsp-mode
  :after goto-addr
  :commands lsp
  :custom
  (lsp-headerline-breadcrumb-icons-enable t)
  (lsp-headerline-breadcrumb-enable t)
  (lsp-modeline-code-action-fallback-icon "✶")
  (lsp-completion-provider :none)
  :bind (:map
         lsp-mode-map
         ("C-x l l" . lsp-ui-doc-glance)
         ("C-." . lsp-find-definition)
         ("C-," . xref-go-back)
         ("C-x ." . kimim/lsp-find-definition-other-window)
         ("C-x l t s" . lsp-treemacs-symbols))
  :config
  (require 'dash)
  (require 'goto-addr)
  (define-key lsp-mode-map (kbd "C-x l") lsp-command-map)
  (add-hook 'xref-after-return-hook 'recenter)
  (defun kimim/lsp-find-definition-other-window ()
    (interactive)
    (lsp-find-definition :display-action 'window)
    (other-window 1)))
#+end_src

** lsp-ui

UI helper for lsp-mode.

#+begin_src emacs-lisp
(use-package lsp-ui
  :commands lsp-ui-mode
  :custom
  (lsp-ui-doc-text-scale-level -1)
  (lsp-ui-doc-show-with-cursor nil)
  (lsp-ui-doc-show-with-mouse nil)
  (lsp-ui-sideline-show-code-actions t)
  (lsp-ui-doc-alignment 'window)
  (lsp-ui-doc-max-width 90))
#+end_src

** eldoc

#+begin_src emacs-lisp
(use-package eldoc
  :hook (prog-mode . eldoc-mode)
  :diminish eldoc-mode)
#+end_src

** cmake mode

#+begin_src emacs-lisp
(use-package cmake-mode
  :config
  (delete '("CMakeLists\\.txt\\'" . cmake-mode) auto-mode-alist)
  (setq auto-mode-alist
        (append '(("CMakeLists\\.txt\\'" . cmake-mode))
                auto-mode-alist)))
#+end_src

* Programming Language
** C and C++

#+begin_src emacs-lisp
(use-package clang-format)
#+End_src

#+begin_src emacs-lisp
(use-package lsp-clangd
  :ensure lsp-mode
  :custom (lsp-clients-clangd-args
           '("--header-insertion-decorators=0"
             "--all-scopes-completion"
             "--completion-style=detailed"
             "--background-index"
             "--clang-tidy"
             "--header-insertion=iwyu"
             "--pch-storage=memory"
             "-j=12")))
#+end_src

~lsp-clangd~ cannot find definition before visiting the ~.c~ file, so we
will use ~ggtags~ to locate the definition in ~.c~ file. After that,
~lsp-clangd~ can index ~.c~ files.

#+begin_src emacs-lisp
(use-package ggtags
  :bind (:map ggtags-mode-map
         ("M-." . ggtags-find-definition)
         :map ggtags-navigation-mode-map
         ("M-o" . other-window)
         ("M-<" . beginning-of-buffer)
         ("M->" . end-of-buffer))
  :hook ((c-mode c++-mode) . ggtags-mode)
  :config
  (setq ggtags-global-ignore-case t)
  (setq ggtags-sort-by-nearness t))
#+end_src

#+begin_src emacs-lisp
(use-package cc-mode
  :ensure nil
  :custom   (c-default-style
             '((java-mode . "java")
               (awk-mode . "awk")
               (c-mode . "cc-mode")
               (c++-mode . "stroustrup++")
               (other . "k&r")))
  :defines c++-mode-map
  :bind (:map
         c++-mode-map
         ("C-<tab>" . clang-format))
  :hook ((c-mode c++-mode cc-mode) . lsp-mode)
  :config
  (require 'clang-format)
  (add-to-list 'auto-mode-alist '("\\.c\\'" . c-mode))
  (add-hook 'c-mode-common-hook
            (lambda ()
              ;;(c-set-style "gnu")
              ;;(c-toggle-auto-newline 0)
              ;;(c-toggle-auto-hungry-state 0)
              ;;(c-toggle-syntactic-indentation 1)
              ;;(highlight-indentation-mode 1)
              (local-set-key "\C-co" 'ff-find-other-file)))
  ;; +   `c-basic-offset' times 1
  ;; -   `c-basic-offset' times -1
  ;; ++  `c-basic-offset' times 2
  ;; --  `c-basic-offset' times -2
  ;; *   `c-basic-offset' times 0.5
  ;; /   `c-basic-offset' times -0.5
  (c-add-style "stroustrup++"
               '("stroustrup"
                 (c-basic-offset . 4)
                 (c-offsets-alist
                  (topmost-intro . 0)
                  (inclass . +)
                  (innamespace . -)
                  (access-label . /)))))
#+end_src

#+begin_src emacs-lisp
(use-package ob-C
  :ensure nil
  :config
  (add-to-list 'org-src-lang-modes '("C" . c))
  (add-to-list 'org-babel-load-languages '(C . t)))
#+end_src

#+begin_src emacs-lisp
(use-package hideif
  :hook ((c-mode c++-mode) . hide-ifdef-mode)
  :config
  (when (eq system-type 'gnu/linux)
    (add-to-list 'hide-ifdef-env '(__linux__ . 1))
    (add-to-list 'hide-ifdef-env '(__GNUC__ . 11)))
  (when (eq system-type 'darwin)
    (add-to-list 'hide-ifdef-env '(__APPLE__ . 1))
    (add-to-list 'hide-ifdef-env '(__clang__ . 1))
    (add-to-list 'hide-ifdef-env '(__llvm__ . 1)))
  (when (eq system-type 'windows-nt)
    (add-to-list 'hide-ifdef-env '(__MINGW32__ . 1))
    (add-to-list 'hide-ifdef-env '(_WIN32 . 1))
    (add-to-list 'hide-ifdef-env '(__GNUC__ . 1)))
  :custom
  (hide-ifdef-initially nil)
  (hide-ifdef-shadow t))
#+end_src

** C#

#+begin_src emacs-lisp
(use-package csharp-mode
  :ensure nil
  :mode ("\\.cs\\'" . csharp-mode))
#+end_src


** Clojure

Clojure[fn:12] is a lisp over JVM. Emm, I like it.

#+begin_src emacs-lisp
(use-package clojure-mode
  :mode (("\\.cljs\\'" . clojurescript-mode)
         ("\\.\\(clj\\|dtm\\|edn\\)\\'" . clojure-mode)
         ("\\.cljc\\'" . clojurec-mode)
         ("\\(?:build\\|profile\\)\\.boot\\'" . clojure-mode))
  :hook (clojure-mode . lsp-mode)
  :config
  (require 'cider)
  (require 'company)
  (require 'flycheck)
  (require 'flycheck-clj-kondo)
  (require 'clj-refactor)
  (require 'clojure-snippets)
  (require 'clojure-mode-extra-font-locking)
  (require 'lsp-mode)
  (add-hook 'cider-repl-mode-hook #'company-mode)
  (add-hook 'cider-mode-hook #'company-mode)
  (add-hook 'clojure-mode-hook #'cider-mode)
  (add-hook 'clojure-mode-hook #'lsp)
  (add-hook 'clojure-mode-hook #'clj-refactor-mode)
  (add-hook 'clojurec-mode-hook #'cider-mode)
  (add-hook 'clojurec-mode-hook #'lsp)
  (add-hook 'clojurec-mode-hook #'clj-refactor-mode)
  (add-hook 'clojurescript-mode-hook #'cider-mode)
  (add-hook 'clojurescript-mode-hook #'lsp)
  (add-hook 'clojurescript-mode-hook #'clj-refactor-mode))
#+end_src

#+begin_src emacs-lisp
(use-package clojure-snippets)
(use-package clojure-mode-extra-font-locking)
#+end_src

*** clj-kondo

Install with npm:

#+begin_src shell
npm install -g clj-kondo
#+end_src

#+begin_src emacs-lisp
(use-package flycheck-clj-kondo)
#+end_src

*** Cider

Cider[fn:13] extends Emacs with support for interactive programming
in Clojure.

#+begin_src emacs-lisp
(use-package cider
  :functions tramp-dissect-file-name
  :custom ((cider-clojure-cli-command "clojure")
           (nrepl-use-ssh-fallback-for-remote-hosts t)
           (nrepl-sync-request-timeout 100))
  :config
  ;;(setq cider-interactive-eval-output-destination 'output-buffer)
  (defun nrepl--ssh-tunnel-command (ssh dir port)
    "Command string to open SSH tunnel to the host associated with DIR's PORT."
    (with-parsed-tramp-file-name dir v
      ;; this abuses the -v option for ssh to get output when the port
      ;; forwarding is set up, which is used to synchronise on, so that
      ;; the port forwarding is up when we try to connect.
      (format-spec
       "%s -v -N -L %p:localhost:%p %u'%h' %x"
       `((?s . ,ssh)
         (?p . ,port)
         (?h . ,v-host)
         (?u . ,(if v-user (format "-l '%s' " v-user) ""))
         (?x . "-o \"ProxyCommand=nc -X connect -x 127.0.0.1:1080 %h %p\""))))))
#+end_src

#+begin_src emacs-lisp
(use-package ob-clojure
  :ensure org
  :custom (org-babel-clojure-backend 'cider)
  :config
  (add-to-list 'org-src-lang-modes '("clojure" . clojure))
  (add-to-list 'org-babel-load-languages '(clojure . t)))
#+end_src

*** clj-refactor

#+begin_src emacs-lisp
(use-package clj-refactor
  :config
  (setq clojure-thread-all-but-last t)
  (cljr-add-keybindings-with-prefix "C-c r")
  (define-key clj-refactor-map "\C-ctf" #'clojure-thread-first-all)
  (define-key clj-refactor-map "\C-ctl" #'clojure-thread-last-all)
  (define-key clj-refactor-map "\C-cu" #'clojure-unwind)
  (define-key clj-refactor-map "\C-cU" #'clojure-unwind-all)
  (add-to-list 'cljr-magic-require-namespaces '("s"  . "clojure.string")))
#+end_src

** Java

#+begin_src emacs-lisp
(use-package dap-mode)
(use-package lsp-java)
#+end_src

** Python

Python development configuration is quite easy. =elpy= [fn:14] is used here:

#+begin_src emacs-lisp
  (use-package elpy
    :config
    (elpy-enable))

  (use-package python
    :ensure nil
    :defines elpy-rpc-backend
    :mode ("\\.py\\'" . python-mode)
    :interpreter ("python" . python-mode)
    :config
    (add-hook 'python-mode-hook
              (lambda ()
                (setq yas-indent-line nil)))
    (add-to-list 'python-shell-completion-native-disabled-interpreters "python"))

  (use-package company-jedi
    :config
    (setq elpy-rpc-backend "jedi"))
#+end_src

Following =python= package is required according to =elpy= mannual:

#+begin_src shell
pip install rope
pip install jedi
# flake8 for code checks
pip install flake8
# importmagic for automatic imports
pip install importmagic
# and autopep8 for automatic PEP8 formatting
pip install autopep8
# and yapf for code formatting
pip install yapf
# install virtualenv for jedi
pip install virtualenv
#+end_src

** Rust

The easiest way to install rust is to run following script:

#+begin_src shell
curl https://sh.rustup.rs -sSf | sh
#+end_src

#+begin_src emacs-lisp
(use-package rustic
  :hook ((rustic-mode . lsp-mode)
         (rustic-mode . (lambda ()
	                      (set (make-local-variable 'compile-command)
		                       "cargo run")))))
#+end_src

** Swift

#+begin_src emacs-lisp
  (use-package swift-mode
    :mode ("\\.swift\\'" . swift-mode))
#+end_src

** Golang
Open =.go= file with go-mode.

#+begin_src emacs-lisp
(use-package go-mode
  :mode ("\\.go\\'" . go-mode)
  :hook (go-mode . lsp-mode))
#+end_src

** Docker file

Some dockerfile is not end with =.dockerfile=, so lets guess:

#+begin_src emacs-lisp
(use-package dockerfile-mode
  :mode ("\\dockerfile\\'" . dockerfile-mode))
#+end_src

~lsp-docker~ requires ~yaml~:

#+begin_src emacs-lisp
(use-package yaml)
#+end_src

** Emacs lisp

#+begin_src emacs-lisp
(use-package elisp-mode
  :ensure nil
  :mode ("\\.el\\'" . emacs-lisp-mode)
  :config
  (define-derived-mode lisp-interaction-mode emacs-lisp-mode "ᴧ"))
#+end_src

** AutoHotKey

=ahk-mode= developed by Rich Alesi[fn:15]

#+begin_src emacs-lisp
  (use-package ahk-mode
    :mode ("\\.ahk\\'" . ahk-mode))
#+end_src

** yaml mode

#+begin_src emacs-lisp
(use-package yaml-mode
  :mode ("\\.yml\\'" . yaml-mode)
  :bind (:map
         yaml-mode-map
         ("\C-m" . newline-and-indent)))
#+end_src

** shell

#+begin_src emacs-lisp
(use-package shell
  :mode ("\\.sh\\'" . shell-script-mode))
#+end_src

#+begin_src emacs-lisp
(use-package ob-shell
  :ensure nil
  :config
  (require 'shell)
  (add-to-list 'org-src-lang-modes '("shell" . shell))
  (add-to-list 'org-babel-load-languages '(shell . t)))
#+end_src

** powershell

#+begin_src emacs-lisp
(use-package powershell
  :mode ("\\.ps1\\'" . powershell-mode))
#+end_src

** solidity

Major mode to edit ethereum solidity smart contract code.

#+begin_src emacs-lisp
(use-package solidity-mode
  :mode ("\\.sol\\'" . solidity-mode))
#+end_src

* Diagram
** PlantUML

That's fun to draw UML with =ob-plantuml= inside =orgmode=:

For Windows Cygwin, install =graphviz= in =cygwin= setup tool

For macOS, install =graphviz= with homebrew:
#+begin_src shell
brew install graphviz
#+end_src

Download =plantuml.jar= from https://plantuml.com/download, and put it to some
place and assign ~plantuml-jar-path~ to there.

#+begin_src emacs-lisp
(use-package plantuml-mode
  :custom
  (plantuml-output-type "svg")
  (plantuml-default-exec-mode 'jar)
  (plantuml-jar-path (expand-file-name
                      (concat kimim/path-kimikit "plantuml/plantuml.jar")))
  (plantuml-executable-args '("-charset" "UTF-8"))
  :config
  (add-to-list 'auto-mode-alist '("\\.puml\\'" . plantuml-mode)))
#+end_src

#+begin_src emacs-lisp
  (use-package ob-plantuml
    :ensure nil
    :config
    (require 'plantuml-mode)
    ;; WARNING: if variables are from other package, setq them at :config
    (setq org-plantuml-jar-path plantuml-jar-path)
    (setq org-plantuml-args plantuml-executable-args)
    (add-to-list 'org-src-lang-modes '("plantuml" . plantuml))
    (add-to-list 'org-babel-load-languages '(plantuml . t)))
#+end_src

I want to preview plantuml result in a full window, so I set
~image-auto-resize~ to ~fit-window~. If the image is still to small, when
it is a long sequence diagram, I can use key ~s i~ to invoke
~image-transform-fit-to-width~ to maximize the width of the image.

#+begin_src emacs-lisp
(use-package image-mode
  :ensure nil
  :custom
  (image-auto-resize 'fit-window)
  :config
  (add-to-list 'auto-mode-alist '("\\.svg\\'" . image-mode)))
#+end_src

** ditaa

#+begin_src emacs-lisp
(use-package ob-ditaa
  :ensure nil
  :custom
  (org-ditaa-jar-path
        (expand-file-name
         (concat kimim/path-kimikit "ditaa/ditaa.jar")))
  :config
  (add-to-list 'org-src-lang-modes '("ditaa" . artist))
  (add-to-list 'org-babel-load-languages '(ditaa . t)))
#+end_src

** Mermaid

Mermaid [fn:16] is another js based diagramming and charting tool. To
use it inside orgmode, mermaid-cli should be installed:

#+begin_src shell
yarn add @mermaid-js/mermaid-cli
#+end_src

~mmdc~ will be installed at ~~/node_modules/.bin/mmdc~. Then we just setup
~ob-mermaid~ and ~mermaid-mode~ for babel evaluation and editing.

#+begin_src emacs-lisp
(use-package ob-mermaid
  :custom
  (ob-mermaid-cli-path "~/node_modules/.bin/mmdc.cmd")
  :config
  (add-to-list 'org-babel-load-languages '(mermaid . t)))
#+end_src

#+begin_src emacs-lisp
(use-package mermaid-mode
  :mode ("\\.mermaid\\'" . mermaid-mode))
#+end_src

* Music

** lilypond

#+begin_src emacs-lisp
(eval-and-compile
  (defun lilypond-load-path ()
    (cond ((eq system-type 'darwin)
           "/usr/local/share/emacs/site-lisp/lilypond")
          ((eq system-type 'windows-nt)
           (concat kimim/path-kimikit "/LilyPond/usr/share/emacs/site-lisp"))
          ((eq system-type 'gnu/linux)
           "/usr/local/share/emacs/site-lisp/lilypond"))))

(use-package lilypond-mode
  :load-path (lambda () (list (lilypond-load-path))))
#+end_src

#+begin_src emacs-lisp
(use-package ob-lilypond
  :ensure nil
  :config
  (require 'lilypond-mode)
  (defun org-babel-lilypond-process-basic (body params)
    "Execute a lilypond block in basic mode."
    (let* ((out-file (cdr (assq :file params)))
           (cmdline (or (cdr (assq :cmdline params))
                        ""))
           (in-file (org-babel-temp-file "lilypond-")))

      (with-temp-file in-file
        (insert (org-babel-expand-body:generic body params)))
      (org-babel-eval
       (concat
        org-babel-lilypond-ly-command
        " -dbackend=eps "
        "-dno-gs-load-fonts "
        "-dinclude-eps-fonts -dno-print-pages -dpreview "
        (or (cdr (assoc (file-name-extension out-file)
                        '(("pdf" . "--pdf ")
                          ("ps" . "--ps ")
                          ("svg" . "--svg ")
                          ("png" . "--png "))))
            "--png ")
        "--output="
        (file-name-sans-extension out-file)
        " "
        cmdline " "
        in-file
        ;; copy .preview file to sans .preview file
        " && mv -f "
        (file-name-sans-extension out-file)
        ".preview."
        (file-name-extension out-file)
        " "
        out-file) "")) nil))
#+end_src

* Reference management
** pdf-tools

Hook ~pdf-view-themed-minor-mode~ to ~pdf-view-mode~ and add
~pdf-view-refresh-themed-buffer~ to ~enable-theme-functions~ to follow
emacs theme color.

#+begin_src emacs-lisp
(use-package pdf-tools
  :functions (pdf-view-refresh-themed-buffer)
  :defines (pdf-view-themed-minor-mode)
  :mode ("\\.pdf\\'" . pdf-view-mode)
  :hook (pdf-view-mode . pdf-view-themed-minor-mode)
  :custom
  (pdf-view-display-size 'fit-height)
  (enable-theme-functions
           '((lambda (theme)
               (require 'pdf-tools)
               (when pdf-view-themed-minor-mode
                 (pdf-view-refresh-themed-buffer theme)))))
  :bind (:map pdf-view-mode-map
         ("C-s" . isearch-forward)
         ("C-r" . isearch-backward)
         ("C-o" . pdf-occur)
         ("j" . pdf-view-next-line-or-next-page)
         ("k" . pdf-view-previous-line-or-previous-page)
         ("<home>". (lambda ()
                      (interactive)
                      (image-scroll-up -999)))
         ("<end>". (lambda ()
                      (interactive)
                      (image-scroll-up 999)))))
#+end_src

** pdf-view-store

To remember visited pages of PDF files.

#+begin_src emacs-lisp
(use-package pdf-view-restore
  :after pdf-tools
  :custom (pdf-view-restore-filename "~/.emacs.d/.pdf-view-restore")
  :hook (pdf-view-mode . pdf-view-restore-mode))
#+end_src

** pdf-view-pagemark

Add indicator of remaining text when scrolling PDF pages.

#+begin_src emacs-lisp
(use-package pdf-view-pagemark
  :after pdf-tools
  :load-path (lambda ()
               (concat kimim/path-kimim-emacs
                       "site-lisp/pdf-view-pagemark"))
  :hook (pdf-view-mode . pdf-view-pagemark-mode))
#+end_src

** ebib

#+begin_src emacs-lisp
(use-package ebib
  :bind (:map ebib-entry-mode-map
         ("N" . kimim/ebib-open-note)
         :map ebib-index-mode-map
         ("N" . kimim/ebib-open-note))
  :functions (ebib--get-key-at-point
              bibtex-completion-edit-notes)
  :custom
  (ebib-preload-bib-files (list bibtex-completion-bibliography))
  :config
  (defun kimim/ebib-open-note ()
    "open bib note at ebib entry"
    (interactive)
    (bibtex-completion-edit-notes (list (ebib--get-key-at-point)))))
#+end_src

** org-ref
#+begin_src emacs-lisp
(use-package bibtex-completion
  :defines bibtex-completion-bibliography
  :custom
  (bibtex-completion-display-formats
   '((t . "${=key=:30} ${author:36} ${title:*} ${year:4} ${=has-pdf=:1}${=has-note=:1} ${=type=:7}")))
  (bibtex-completion-bibliography (concat kimim/path-docs "references.bib"))
  (bibtex-completion-library-path kimim/path-docs)
  (bibtex-completion-notes-path (concat kimim/path-notes "org-ref-notes.txt"))
  :bind ("C-x m b" .
         (lambda ()
           (interactive)
           (find-file
            (concat kimim/path-docs "references.bib")))))
#+end_src

#+begin_src emacs-lisp
(use-package org-ref
  :functions (-flatten
              f-join
              org-ref-get-bibtex-key-and-file
              bibtex-completion-key-at-point
              bibtex-completion-candidates
              bibtex-completion-init
              bibtex-completion-edit-notes
              org-ref-cite-hydra/body
              org-ref-find-bibliography
              kimim/org-ref-open-pdf
              kimim/org-ref-open-pdf-in-dired
              kimim/org-ref-open-notes
              kimim/org-ref-open-notes-action
              kimim/org-ref-get-pdf-filename
              kimim/org-ref-open-pdf-action
              kimim/org-ref-open-pdf-in-dired-action)
  :bind (("C-x m p" . kimim/org-ref-open-pdf-at-point)
         ("C-x m P" . kimim/org-ref-open-pdf-in-dired-at-point)
         ("C-x m n" . kimim/org-ref-open-notes-at-point)
         :map org-mode-map
         ("C-c ]" . org-ref-insert-cite-link))
  :config
  (require 'bibtex-completion)
  (require 'org-roam-bibtex)
  ;; use MS word to open office file
  (add-to-list 'org-file-apps
               '("\\.docx?\\'" . default))
  (add-to-list 'org-file-apps
               '("\\.pptx?\\'" . default))
  (add-to-list 'org-file-apps
               '("\\.xlsx?\\'" . default))

  ;; dont put [ inside file name
  (defun kimim/org-ref-get-pdf-filename (key)
    (if bibtex-completion-library-path
        (let* ((pdf-dirs (if (listp bibtex-completion-library-path)
                             bibtex-completion-library-path
                           (list bibtex-completion-library-path)))
               (pdfs
                (-flatten
                 (--map (file-expand-wildcards
                         (f-join it (format "%s*" key)))
                        (-flatten
                         (append pdf-dirs
                                 (--map (directory-files-recursively it "" t)
                                        pdf-dirs)))))))
          (cond
           ((= 0 (length pdfs))
            (expand-file-name (format "%s.pdf" key) bibtex-completion-library-path))
           ((= 1 (length pdfs))
            (car pdfs))
           ((> (length pdfs) 1)
            (completing-read "Choose: " pdfs))))
      ;; No bibtex-completion-library-path defined so return just a file name.
      (format "%s.pdf" key)))

  (defun kimim/org-ref-open-pdf-action (key)
    "Open the pdf for bibtex key under point if it exists."
    (let* ((pdf-file (kimim/org-ref-get-pdf-filename key)))
      (if (file-exists-p pdf-file)
          (find-file pdf-file)
        (message "no pdf found for %s" key))))

  (defun kimim/org-ref-open-pdf (&optional arg)
    (interactive)
    (kimim/org-ref-open-pdf-action (org-ref-read-key)))

  (defun kimim/org-ref-open-pdf-at-point ()
    "Open the pdf for bibtex key under point if it exists."
    (interactive)
    (let* ((results (condition-case nil
                        (if (eq 'org-mode
                                (buffer-local-value
                                 'major-mode (current-buffer)))
                            (org-ref-get-bibtex-key-and-file))
                        (error nil))))
      (if (or (null results)
              (string= "" (car results))
              (null (car results)))
          (kimim/org-ref-open-pdf)
        (let ((pdf-file (kimim/org-ref-get-pdf-filename (car results))))
          (if (file-exists-p pdf-file)
              (find-file pdf-file)
            (kimim/org-ref-open-pdf))))))

  (defun kimim/org-ref-open-pdf-in-dired-action (key)
    "Open the pdf dired for bibtex key under point if it exists."
    (let* ((pdf-file (kimim/org-ref-get-pdf-filename key)))
      (if (file-exists-p pdf-file)
          (dired-jump nil pdf-file)
        (message "no pdf found for %s" key))))

  (defun kimim/org-ref-open-pdf-in-dired (&optional arg)
    (interactive)
    (kimim/org-ref-open-pdf-in-dired-action (org-ref-read-key)))

  (defun kimim/org-ref-open-pdf-in-dired-at-point ()
    "Open the pdf dired for bibtex key under point if it exists."
    (interactive)
    (let* ((results (condition-case nil
                        (org-ref-get-bibtex-key-and-file)
                      (error nil)))
           (key (car results)))
      (if (or (string= "" key) (null key))
          (kimim/org-ref-open-pdf-in-dired)
        (let ((pdf-file (kimim/org-ref-get-pdf-filename key)))
          (if (file-exists-p pdf-file)
              (dired-jump nil pdf-file)
            (message "no pdf found for %s" key))))))

  (defun kimim/org-ref-open-notes-action (key)
    "Open the notes for bibtex key under point if it exists."
    (bibtex-completion-edit-notes (list key)))

  (defun kimim/org-ref-open-notes (&optional arg)
    (interactive)
    (kimim/org-ref-open-notes-action (org-ref-read-key)))

  (defun kimim/org-ref-open-notes-at-point ()
    "Open the notes of a reference if they exist."
    (interactive)
    (let* ((results (condition-case nil
                        (org-ref-get-bibtex-key-and-file)
                      (error nil)))
           (key (car results)))
      (if (or (string= "" key) (null key))
          (kimim/org-ref-open-notes)
        (kimim/org-ref-open-notes-action key)))))
#+end_src

There is a built-in =bibtex-mode= to manage references. We can extend it to
support more functions from =org-ref=:

#+begin_src emacs-lisp
(use-package bibtex
  :ensure nil
  :bind (:map bibtex-mode-map
         ("C-x m p" . org-ref-open-bibtex-pdf)
         ("C-x m n" . org-ref-open-bibtex-notes)
         ("C-x m d" . kimim/org-ref-open-bibtex-in-dired)
         ("C-c C-z" . org-ref-open-bibtex-notes))
  :config
  (require 'org-ref)
  (require 'org-roam-bibtex))
#+end_src

** nov: reading epub books

#+begin_src emacs-lisp
(use-package nov
  :mode ("\\.epub\\'" . nov-mode)
  :hook ((nov-mode . olivetti-mode)
         (nov-mode . (lambda ()
                       (visual-line-mode)
                       (setq line-spacing 0.5))))
  :config
  (require 'olivetti)
  (setq nov-text-width olivetti-body-width))
#+end_src

* Orgmode
** org general setting

#+begin_src emacs-lisp
(use-package org
  :mode (("\\.txt\\'" . org-mode)
         ("\\.org\\'" . org-mode))
  :bind
  (:map org-mode-map
   ("C-c b" . org-iswitchb)
   ("C-c l" . org-store-link)
   ("C-c C-x l" . org-toggle-link-display)
   ("C-c  ！" . org-time-stamp-inactive)
   ("C-c  。" . org-time-stamp)
   ("M-." . org-open-at-point)
   ("M-*" . org-mark-ring-last-goto)
   ("M-h" . nil)
   ("C-'" . org-emphasize)
   ("C-c p" . kimim/preview-babel-image))
  :custom
  (org-modules '(org-habit
                 ol-w3m ol-bbdb ol-bibtex
                 ol-docview ol-gnus ol-info
                 ol-irc ol-mhe ol-rmail ol-eww))
  (org-export-with-sub-superscripts '{})
  (org-startup-folded 'showall)
  (org-startup-with-inline-images t)
  (org-tags-column (- fill-column))
  ;; image width in preview
  (org-image-actual-width `(,(* (+ fill-column 10)
                                (frame-char-width))))
  :config
  ;; no use for me, I always press this key accidentally
  (unbind-key "C-'" 'org-mode-map)
  (setq company-minimum-prefix-length 2)
  (setq org-hide-emphasis-markers t)
  (setq org-support-shift-select t)
  ;; no empty line after collapsed
  (setq org-cycle-separator-lines 0)
  (if window-system
      (setq org-startup-indented t)
    (setq org-startup-indented nil)))
#+end_src

** org-appear

#+begin_src emacs-lisp
(use-package org-appear
  :commands (org-appear-mode)
  :ensure t
  :custom (org-appear-autolinks nil)
  :hook (org-mode . org-appear-mode))
#+end_src

** org-superstar

#+begin_src emacs-lisp
(use-package org-superstar
  :ensure t
  :hook ((org-mode . org-superstar-mode)
         (org-mode . (lambda ()
                       "Beautify Org Checkbox Symbol"
                       (push '(":category:" . "▲") prettify-symbols-alist)
                       (push '("[X]" . "☑" ) prettify-symbols-alist)
                       (push '("[ ]" . "☐" ) prettify-symbols-alist)
                       (push '("#+begin_src" . "«" ) prettify-symbols-alist)
                       (push '("#+end_src" . "»" ) prettify-symbols-alist)
                       ;;(push '("#+begin_quotation" . "❝" ) prettify-symbols-alist)
                       ;;(push '("#+end_quotation" . "❞" ) prettify-symbols-alist)
                       (prettify-symbols-mode))))
  :custom
  (org-superstar-remove-leading-stars t)
  (org-superstar-headline-bullets-list
   '(9673 9675 9679 9632))
  (org-superstar-item-bullet-alist
   '((42 . 9679) (43 . 9830) (45 . 9644))))
#+end_src

** org-indent

#+begin_src emacs-lisp
(use-package org-indent
  :ensure nil
  :hook (org-mode . org-indent-mode)
  :diminish org-indent-mode
  :config
  (defun org-indent--compute-prefixes ()
    "Compute prefix strings for regular text and headlines."
    (setq org-indent--heading-line-prefixes
	      (make-vector org-indent--deepest-level nil))
    (setq org-indent--inlinetask-line-prefixes
	      (make-vector org-indent--deepest-level nil))
    (setq org-indent--text-line-prefixes
	      (make-vector org-indent--deepest-level nil))
    (dotimes (n org-indent--deepest-level)
      (let ((indentation (if (<= n 1) 0
			               (* (1- org-indent-indentation-per-level)
			                  (1- n)))))
        ;; Headlines line prefixes.
        (let ((heading-prefix (make-string indentation ?*)))
	      (aset org-indent--heading-line-prefixes
	            n
	            (org-add-props heading-prefix nil 'face 'org-indent))
	      ;; Inline tasks line prefixes
	      (aset org-indent--inlinetask-line-prefixes
	            n
	            (cond ((<= n 1) "")
		              ((bound-and-true-p org-inlinetask-show-first-star)
		               (concat org-indent-inlinetask-first-star
			                   (substring heading-prefix 1)))
		              (t (org-add-props heading-prefix nil 'face 'org-indent)))))
        ;; Text line prefixes.
        (let ((remove-space (if (> n 0)
                                (- n 1)
                              0)))
          (aset org-indent--text-line-prefixes
	            n
	            (org-add-props
		            (concat (make-string (- (+ n indentation) remove-space) ?\s)
			                (and (> n 0)
			                     (char-to-string org-indent-boundary-char)))
		            nil 'face 'org-indent)))))))
#+end_src

** org-inline-pdf

~org-inline-pdf-mode~ includes a page from a PDF file. It need ~pdf2svg~
to extract a page.

Install ~pdf2svg~ for msys64/mingw64:

#+begin_src shell
pacman -S mingw-w64-x86_64-pdf2svg
#+end_src

#+begin_src emacs-lisp
(use-package org-inline-pdf
  :after org
  :hook (org-mode . org-inline-pdf-mode))
#+end_src

Then we can include a PDF file like this:

#+ATTR_ORG: :width 60% :page 3
[[~/docs/README.pdf]]

** orgalist

#+begin_src emacs-lisp
  (use-package orgalist
    :commands (orgalist-mode))
#+end_src

** org for writing

#+begin_src emacs-lisp
(use-package org-download
  :commands (org-download-enable)
  :custom
  (org-download-heading nil)
  (org-structure-template-alist '(("a" . "export ascii")
                                  ("c" . "center")
                                  ("C" . "comment")
                                  ("e" . "example")
                                  ("E" . "export")
                                  ("h" . "export html")
                                  ("l" . "export latex")
                                  ("q" . "quote")
                                  ("Q" . "quotation")
                                  ("s" . "src")
                                  ("v" . "verse")))
  :functions kimim/org-download-annotate
  :config
  (setq org-download-timestamp "")
  (setq-default org-download-image-dir "./images")
  (setq org-download-method 'directory)

  (defun kimim/org-download-annotate (link)
    "Annotate LINK with the time of download."
    (format "#+NAME: fig:%s\n#+CAPTION: %s\n"
            (file-name-base link) (file-name-base link)))
  (setq org-download-annotate-function #'kimim/org-download-annotate)
  (setq org-download-display-inline-images nil)
  (setq image-file-name-extensions
        (quote
         ("png" "jpeg" "jpg" "gif" "tiff" "tif" "xbm"
          "xpm" "pbm" "pgm" "ppm" "pnm" "svg" "pdf" "bmp")))
  (defun org-download--dir-2 () "."))
#+end_src

#+begin_src emacs-lisp
(use-package org
  :custom (org-num-skip-footnotes t)
  :config
  (require 'org-download)
  (setq org-hide-leading-stars t)
  (setq org-footnote-auto-adjust t)
  (setq org-footnote-define-inline nil))
#+end_src

** org with source code

#+begin_src emacs-lisp
(use-package org
  :config
  (setq org-src-window-setup 'current-window)
  (setq org-src-fontify-natively t)
  (setq org-src-preserve-indentation t)
  (setq org-edit-src-content-indentation 0)
  (setq org-confirm-babel-evaluate nil)
  (add-hook 'org-babel-after-execute-hook 'org-display-inline-images)
  ;; load diagram languages, I frequently use in orgmode
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((plantuml . t)
     (ditaa . t)
     (clojure .t)))
  ;; get the idea from John Kitchen(author of org-ref):
  ;; https://emacs.stackexchange.com/questions/20577/org-babel-load-all-languages-on-demand
  (defadvice org-babel-execute-src-block (around load-language nil activate)
    "Load language if needed"
    (let* ((language (org-element-property :language (org-element-at-point)))
           (language-to-load (if (string= language "C++")
                                 "C"
                               language)))
      (unless (cdr (assoc (intern language-to-load) org-babel-load-languages))
        (require (intern (concat "ob-" language-to-load)))
        (add-to-list 'org-babel-load-languages (cons (intern language-to-load) t))
        (org-babel-do-load-languages 'org-babel-load-languages
                                     org-babel-load-languages))
      ad-do-it)))
#+end_src

** org exporting

When exporting, do not export with author and date.

#+begin_src emacs-lisp
(use-package org
  :bind ("C-c C-'" . org-insert-structure-template)
  :functions (org-export--collect-headline-numbering
              org-export--get-min-level)
  :custom
  (org-export-allow-BIND t)
  (org-export-html-validation-link nil)
  ;;(org-export-with-sub-superscripts '{})
  (org-export-with-author nil)
  (org-export-with-date t)
  :config
  (require 'ox-latex)
  (defun org-export--collect-tree-properties (data info)
    "Extract tree properties from parse tree.

DATA is the parse tree from which information is retrieved.  INFO
is a list holding export options.

Following tree properties are set or updated:

`:headline-offset' Offset between true level of headlines and
     local level.  An offset of -1 means a headline
     of level 2 should be considered as a level
     1 headline in the context.

`:headline-numbering' Alist of all headlines as key and the
        associated numbering as value.

`:id-alist' Alist of all ID references as key and associated file
            as value.

Return updated plist."
    ;; Install the parse tree in the communication channel.
    (setq info (plist-put info :parse-tree data))
    ;; Compute `:headline-offset' in order to be able to use
    ;; `org-export-get-relative-level'.
    (setq info
          (plist-put info
                     :headline-offset
                     (- 1 (org-export--get-min-level data info))))
    ;; From now on, properties order doesn't matter: get the rest of the
    ;; tree properties.
    (org-combine-plists
     info
     (list :headline-numbering (org-export--collect-headline-numbering data info)
           :id-alist
           (org-element-map data 'link
             (lambda (l)
               (and (string= (org-element-property :type l) "id")
                    (let* ((id (org-element-property :path l))
                           (file (car (org-id-find id))))
                      ;; replace txt extension with exported PDF file
                      (and file
                           (let ((pdf-file (concat (file-name-sans-extension file) ".pdf")))
                             (if (file-exists-p pdf-file)
                                 (cons id (file-relative-name pdf-file))
                               (cons id (file-relative-name file)))))))))))))
#+end_src

*** org to pdf

LaTeX is required to convert =org-mode= to PDF.

For MacOS:

#+begin_src shell
brew cask install mactex-no-gui
#+end_src

For Windows, there are three options:

1. download and install CTEX from http://www.ctex.org
2. install texlive-collection in cygwin
   #+begin_src shell
apt-cyg install texlive-collection-xetex    \
        texlive-collection-latex            \
        texlive-collection-fontsrecommended
   #+end_src
3. download and install texlive from [[http://tug.org/texlive/acquire-netinstall.html][tug.org]]

For Linux, download texlive install pacakge from [[http://mirror.ctan.org/systems/texlive/tlnet/install-tl-unx.tar.gz][ctan.org]]

#+begin_src shell
tar zxvf install-tl-unx.tar.gz
cd install-tl-20200908/
sudo ./install-tl
#+end_src

Then for all the OS platforms, use =tlmgr= to install user level tex
packages (notes that, in windows, you may need to run =tlmgr.bat=):

#+begin_src shell
tlmgr init-usertree
tlmgr --usermode install ctex titlesec enumitem ms fontspec abstract    \
                         zhnumber fandol lastpage pdftexcmds infwarerr  \
                         minted fvextra etoolbox fancyvrb upquote       \
                         lineno catchfile xstring framed float          \
                         grffile wrapfig ulem lettrine minifp           \
                         capt-of xcolor svg koma-script trimspaces      \
                         titling layaureo parskip extsizes pgf          \
                         moderncv microtype
fmtutil-sys --all
#+end_src

Recently, I adopted to mainly use texlive on Windows. It works fine and provide
a GUI tool to maintain packages: ~tlshell.exe~. You can use it to install and
update latex packages.

To export =org-mode= to PDF, with code style highlight, you need to install
=python= and =pygments=. Because =pygmentize= from =pygments= is used to
generate =latex= markups for font highlighting.

For MacOS, the OS shipped =python2.7= does not accompanied with =pip= package
management script. So you need to install =pip=, and then add =pygments=,
acc. https://pip.pypa.io/en/stable/installing/ , =pygmentize= will be installed
under =$HOME/Library/Python/2.7/bin=, which is added to =exec-path= and =PATH.=

#+begin_src shell
curl https://bootstrap.pypa.io/get-pip.py -o get-pip.py
python get-pip.py
#+end_src

Get =pygments= with =pip=:

#+begin_src shell
pip install pygments
#+end_src

For Ubuntu Linux:

#+begin_src shell
sudo apt install python3-pygments
#+end_src

#+begin_src emacs-lisp
(use-package latex-classes
  :load-path "~/kimim-emacs/site-lisp"
  :config
  (require 'bibtex-completion))
#+end_src

#+begin_src emacs-lisp
(use-package ox-latex
  :ensure nil
  :defines (kimim/latex-classes
            org-beamer-frame-level
            org-latex-minted-langs)
  :functions (org-html--make-attribute-string)
  :commands org-latex-publish-to-pdf
  :diminish org-beamer-mode
  :custom
  (org-latex-listings 'minted)
  (org-latex-minted-options
   '(("frame" "single") ("fontsize" "\\small")))
  (org-latex-pdf-process
   '("latexmk -xelatex -shell-escape -output-directory=%o %F"))
  ;; avoid warning when preview tikz in org babel
  (org-babel-latex-preamble
      (lambda (_)
        "\\documentclass[preview]{standalone}"))
  :config
  (require 'org-ref)
  (require 'ox)
  (require 'ox-beamer)
  (require 'latex-classes)
  (setq org-startup-with-beamer-mode t)
  (setq org-beamer-frame-level 2)
  ;; export quotes match
  (setq org-export-with-smart-quotes t)
  (add-to-list 'org-latex-minted-langs
               '(plantuml "text"))
  (add-to-list 'org-latex-minted-langs
               '(ditaa "text"))
  (add-to-list 'org-latex-minted-langs
               '(conf "text"))
  (add-to-list 'org-latex-minted-langs
               '(mermaid "text"))
  (add-to-list 'org-latex-minted-langs
               '(lilypond "text"))
  (defun ref-headline-removal (backend)
    "Remove reference headline with tag: ref"
    (org-map-entries
     (lambda ()
       (when (member "ref" org-scanner-tags)
         (delete-region (point) (line-beginning-position 2))))))
  (add-hook 'org-export-before-parsing-functions 'ref-headline-removal)

  ;;;;; Nicolas Goaziou, http://article.gmane.org/gmane.emacs.orgmode/67692
  ;; (defun org-latex-ignore-heading-filter-headline (headline backend info)
  ;;   "Strip headline from HEADLINE. Ignore BACKEND and INFO."
  ;;   (when (and (org-export-derived-backend-p backend 'latex)
  ;;              (string-match "\\`.*ignoreheading.*\n" headline))
  ;;     (replace-match "" nil nil headline)))
  ;; (add-to-list 'org-export-filter-headline-functions
  ;;              'org-latex-ignore-heading-filter-headline)

  ;; most of the time, I do not need table of contents
  (setq org-latex-toc-command nil)
  ;; https://www.tuicool.com/articles/ZnAnym
  ;; remove error: ! LaTeX Error: Command \nobreakspace unavailable in encoding T1.
  ;; add: \DeclareRobustCommand\nobreakspace{\leavevmode\nobreak\ }
  ;; put long latex classes in a separate file
  (require 'latex-classes)
  (setq org-latex-classes kimim/latex-classes)
  (setq org-latex-default-class "article")
  ;; remove fontenc, and AUTO in front of inputenc,
  ;; then francais can be processed
  (setq org-latex-default-packages-alist
        (quote
         (("" "inputenc" t ("pdflatex"))
          ("" "minted" t nil)
          ("" "amsfonts" t nil)
          ("" "graphicx" t nil)
          ("inkscapeopt = -C --export-ignore-filters, inkscapelatex=false" "svg" t nil)
          ("" "grffile" t nil)
          ("" "longtable" nil nil)
          ("" "wrapfig" nil nil)
          ("" "rotating" nil nil)
          ("normalem" "ulem" t nil)
          ("" "amsmath" t nil)
          ("" "textcomp" t nil)
          ("" "lettrine" t nil)
          ("" "capt-of" nil nil))))
  ;; latex preview report exception with bibref
  ;; (setq org-latex-packages-alist
  ;;       `(,(concat "\\addbibresource{"
  ;;                  (expand-file-name bibtex-completion-bibliography)
  ;;                  "}")))
  ;; (mapconcat
  ;;  (lambda (it)
  ;;    (concat "\\addbibresource{" (expand-file-name it) "}\n"))
  ;;  bibtex-completion-bibliography)

  ;; preview latex equation with SVG image
  (add-to-list 'org-preview-latex-process-alist
               '(xdvsvgm :progams
					     ("xelatex" "dvisvgm")
					     :discription "xdv > svg"
					     :message "you need install the programs: xelatex and dvisvgm."
					     :image-input-type "xdv"
					     :image-output-type "svg"
					     :image-size-adjust (1.3 . 1.3)
					     :latex-compiler ("xelatex -no-pdf -shell-escape -output-directory=%o %f")
					     :image-converter ("dvisvgm %f -n -b min -c %S -o %O")))
  (setq org-preview-latex-default-process 'xdvsvgm))
#+end_src

*** org to html page

#+begin_src emacs-lisp
(use-package ox-html
  :ensure org
  :functions (org-html-encode-plain-text
              org-html-close-tag
              f-read)
  :commands (org-html-publish-to-html)
  :config
  (setq org-html-validation-link nil)
  (defadvice org-html-paragraph (before fsh-org-html-paragraph-advice
                                        (paragraph contents info) activate)
    "Join consecutive Chinese lines into a single long line without
  unwanted space when exporting org-mode to html."
    (let ((fixed-contents)
          (orig-contents (ad-get-arg 1))
          (reg-han "[[:multibyte:]]"))
      (setq fixed-contents (replace-regexp-in-string
                            (concat "\\(" reg-han "\\) *\n *\\(" reg-han "\\)")
                            "\\1\\2" orig-contents))
      (ad-set-arg 1 fixed-contents)))
  ;; embed svg image to html file
  (defun org-html--format-image (source attributes info)
    "Return \"img\" tag with given SOURCE and ATTRIBUTES.
SOURCE is a string specifying the location of the image.
ATTRIBUTES is a plist, as returned by
`org-export-read-attribute'.  INFO is a plist used as
a communication channel."
    (if (string= "svg" (file-name-extension source))
        ;;(plist-get attributes :embed-svg)
        (format
         "<div align=\"center\">%s</div>" (f-read source))
      (org-html-close-tag
       "img"
       (org-html--make-attribute-string
        (org-combine-plists
         (list :src source
               :alt (if (string-match-p
                         (concat "^" org-preview-latex-image-directory) source)
                        (org-html-encode-plain-text
                         (org-find-text-property-in-string 'org-latex-src source))
                      (file-name-nondirectory source)))
         (if (string= "svg" (file-name-extension source))
             (org-combine-plists '(:class "org-svg") attributes '(:fallback nil))
           attributes)))
       info))))
#+end_src

*** org to html slides

#+begin_src emacs-lisp
  (use-package org-re-reveal
    :bind ("C-x r v" . org-re-reveal-export-to-html-and-browse)
    :config
    (use-package htmlize :ensure t)
    (setq org-re-reveal-root "https://cdn.jsdelivr.net/npm/reveal.js@3.9.2/")
    (setq org-re-reveal-theme "none")
    (setq org-re-reveal-width 1000)
    (setq org-re-reveal-height 750)
    (setq org-re-reveal-transition "none")
    (setq org-re-reveal-hlevel 2)
    (setq org-re-reveal-extra-css "./kimim.css"))
#+end_src

** org link: page in pdf

#+begin_src emacs-lisp
(use-package org
  :functions org-supdf-open
  :config
  (org-link-set-parameters "supdf"
                           :follow #'org-supdf-open)

  (defun org-supdf-open (path)
    "Visit the pdf page"
    (let ((file-page (split-string path "::")))
      (cond
       ((eq system-type 'windows-nt)
        (progn
          (w32-shell-execute
           "open" (concat kimim/path-kimikit "sumatrapdf/sumatrapdf.exe")
           (concat "\"" (expand-file-name (car file-page))
                   "\" -page " (cadr file-page)))))))))
#+end_src

** org link: onenote

New link to use Office Onenote.

#+begin_src emacs-lisp
(use-package org
  :functions org-onenote-open
  :config
  (org-link-set-parameters "onenote"
                           :follow #'org-onenote-open)

  (defun org-onenote-open (path)
    "Visit the onenote link"
    (cond
     ((eq system-type 'windows-nt)
      (progn
        (w32-shell-execute
         "open" (concat "onenote:" path))))
     ((eq window-system 'ns)
      (shell-command
       (replace-regexp-in-string
        "&" "\\\\&" (format "open onenote:%s" path)))))))
#+end_src

** org publish to jekyll

#+begin_src emacs-lisp
(use-package o2jk
  :load-path "~/kimim-emacs/site-lisp/o2jk"
  :functions (o2jk-input-directory
              org-publish-cache-get-file-property
              org-ref-export-to-file-nomarks-noopen
              org-export-output-file-name
              org-ref-process-buffer
              org-export-expand-include-keyword)
  :commands (o2jk-publish
             o2jk-list-drafts
             o2jk-create-draft)
  :bind (("C-x m k" . o2jk-create-draft)
         ("C-x m l" . o2jk-list-posts))
  :custom ((o2jk-blog-author "kimim")
           (o2jk-source-directory "~/notes/kimi.im/_notes/_posts")
           (o2jk-jekyll-directory "~/notes/kimi.im/_posts")
           (o2jk-jekyll-drafts-dir "~/notes/_draft")
           (o2jk-jekyll-posts-dir ""))
  :config

  (require 'org-ref)

  (setq org-publish-project-alist
        `(("post"  ;; dynamic pages like blog articles
           :base-directory ,(o2jk-input-directory)
           :base-extension "org\\|txt"
           :publishing-directory ,(o2jk-output-directory)
           :publishing-function org-ref-html-publish-to-html
           :headline-levels 4
           :html-head "<link rel=\"stylesheet\" href=\"./css/style.css\" type=\"text/css\"/>"
           :html-preamble t
           :recursive t
           :make-index t
           :html-extension "html"
           :body-only t)
          ("images"
           :base-directory ,(o2jk-input-directory "img")
           :base-extension "jpg\\|gif\\|png"
           :publishing-directory ,(o2jk-output-directory "img")
           :publishing-function org-publish-attachment
           :recursive t)
          ("js"
           :base-directory ,(o2jk-input-directory "js")
           :base-extension "js"
           :publishing-directory ,(o2jk-output-directory "js")
           :publishing-function org-publish-attachment
           :recursive t)
          ("css"
           :base-directory ,(o2jk-input-directory "css")
           :base-extension "css\\|el"
           :publishing-directory ,(o2jk-output-directory "css")
           :publishing-function org-publish-attachment
           :recursive t)
          ("web" :components ("images" "js" "css"))))
  ;; Very simplified version of org-ref-export-to from org-ref-export.el
  ;; that export to filename
  (defun org-ref-export-to-file-nomarks-noopen
      (backend filename &optional async subtreep visible-only body-only info)
    (org-export-with-buffer-copy
     (org-export-expand-include-keyword)
     (org-ref-process-buffer backend subtreep)
     (org-export-to-file backend filename
       async subtreep visible-only
       body-only info)))

  ;; org-html-publish-to-html from ox-html.el adapted to org-ref
  ;; Instead of org-export-to-file calls org-ref-export-to-file-nomarks-noopen
  (defun org-ref-html-publish-to-html (plist filename pub-dir)
    (unless (or (not pub-dir)
                (file-exists-p pub-dir))
      (make-directory pub-dir t))
    ;; Check if a buffer visiting FILENAME is already open.
    (let* ((org-inhibit-startup t)
           (visiting (find-buffer-visiting filename))
           (work-buffer (or visiting (find-file-noselect filename))))
      (unwind-protect
          (with-current-buffer work-buffer
            (let ((output (org-export-output-file-name ".html" nil pub-dir)))
              (org-ref-export-to-file-nomarks-noopen
               'html output
               nil nil nil (plist-get plist :body-only)
               (org-combine-plists
                plist
                `(:crossrefs
                  ,(org-publish-cache-get-file-property
                    ;; Normalize file names in cache.
                    (file-truename filename) :crossrefs nil t)
                  :filter-final-output
                  (org-publish--store-crossrefs
                   org-publish-collect-index
                   ,@(plist-get plist :filter-final-output)))))))))))
#+end_src

* Note Taking
** org-roam

Org-roam implements =zettelkasten= method [fn:17] used by famous German socialogist
Niklas Luhmann[fn:18].

First you should install =sqlite3=, which is used to index the links.

Windows/MSYS2:

#+begin_src shell
pacman -S mingw-w64-x86_64-sqlite3
#+end_src

Windows/Cygwin:

#+begin_src shell
apt-cyg install sqlite3
#+end_src

sqlite3 is shipped in macOS by default.

#+begin_src emacs-lisp
(use-package org-roam
  :functions (orb-get-node-citekey
              orb--new-note
              -contains?)
  :commands (kimim/non-cite-filter)
  :ensure t
  :custom
  (org-roam-directory kimim/path-notes)
  (org-roam-db-location (file-truename
                         (concat user-emacs-directory
                                 "org-roam.db")))
  (org-roam-link-auto-replace nil)
  (org-roam-file-extensions '("txt" "org"))
  (org-roam-node-display-template "${title:70}${tags:30}${refs}")
  (org-roam-capture-templates
   '(("d" "default" plain "%?"
      :if-new
      (file+head
       "%(concat (kimim/genfile-timestamp) \"${slug}.txt\")"
       "#+TITLE: ${title}\n")
      :unnarrowed t)))
  (org-roam-dailies-capture-templates
   '(("d" "default" plain "- /%<%H:%M>/ %?" :target
      (file+datetree "%<%Y>.org" :day))))
  (orb-preformat-keywords '("citekey"))
  :bind
  (("C-c n f" . (lambda ()
                  (interactive)
                  (org-roam-node-find
                   nil nil 'kimim/non-cite-filter)))
   ("C-c n F" . (lambda ()
                  (interactive)
                  (org-roam-node-find
                   nil nil 'kimim/cite-filter)))
   ("C-c n c" . org-roam-capture)
   ("C-c n j" . org-roam-dailies-capture-today)
   ("C-c n ." . org-roam-dailies-goto-today)
   ("C-c n r" . org-roam-ref-add)
   ("C-c n x" . org-roam-node-random)
   :map org-roam-mode-map
   (("C-c n l" . org-roam)
    ("C-c n g" . org-roam-graph))
   :map org-mode-map
   (("C-c n i" . (lambda ()
                   (interactive)
                   (org-roam-node-insert 'kimim/non-cite-filter)))
    ("C-c n I" . (lambda ()
                   (interactive)
                   (org-roam-node-insert 'kimim/cite-filter)))
    ("C-c n t" . org-roam-tag-add)
    ("C-c n a" . org-roam-alias-add)
    ("C-c n g" . org-id-get-create)
    ("C-c n b" . org-roam-buffer-toggle)))
  :config
  (add-to-list 'load-path "~/kimim-emacs/site-lisp")
  (require 'org-roam-dailies)
  (require 'org-roam-bibtex)
  (require 'emacsql)
  (require 'kimim)
  ;; open org link in current window
  (add-to-list
   'org-link-frame-setup
   '(file . find-file))
  ;;(setq org-roam-v2-ack t)
  (setq emacsql-global-timeout 60) ;; default 30 seconds will timeout
  (org-roam-db-autosync-enable)
  (defvar orb-templates
    '(("r" "reference" plain
       "#+ROAM_KEY: %^{citekey}\n\n%?"
       :target
       (file+head
        "references/%(concat (kimim/genfile-timestamp) \"${citekey}.txt\")"
        "#+title: ${title}\n")
       :unnarrowed t)))
  (defun kimim/cite-filter (node)
    (orb-get-node-citekey node))
  (defun kimim/non-cite-filter (node)
    (or
     (not (orb-get-node-citekey node))
     (-contains? (org-roam-node-tags node) "standard")
     (-contains? (org-roam-node-tags node) "terminology")))
  (advice-add 'org-roam-node-visit
              :after (lambda (&rest r) (reposition-window)))

  (advice-add
   #'orb--new-note :around
   (lambda (origin-fun &rest args)
     (let ((org-roam-capture-templates orb-templates))
       (apply origin-fun args)))))
#+end_src

** org-roam-bibtex

It is useful to create reference notes with ~org-roam-bibtex~. ~C-c C-z~ used in
~org-ref~ is calling ~orb-org-ref-edit-note~ to edit ~org-roam~ note.

#+begin_src emacs-lisp
(use-package org-roam-bibtex
  :hook ((org-mode bibtex-mode) . org-roam-bibtex-mode)
  :diminish org-roam-bibtex-mode
  ;; use the original title captalization
  :custom (orb-bibtex-entry-get-value-function
           #'bibtex-completion-get-value)
  :config
  (require 'org-ref))
#+end_src

** Markdown mode

Markdown is widely used as plain text file format. Pandoc [fn:19] can
be used to convert markdown file to html and other formats. We can
download the [[https://github.com/jgm/pandoc/releases/latest][latest version]] and put the binary file to system path,
such as ~/usr/local/bin~, and then set ~markdown-command~ to ~pandoc~.

#+begin_src emacs-lisp
(use-package markdown-mode
  :mode (("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :functions (s-starts-with?
              markdown--get-remote-image)
  :custom ((markdown-hide-urls nil)
           (markdown-command "pandoc")
           (markdown-max-image-size '(1200 . nil)))
  :bind(:map
        markdown-mode-map
        ("C-<tab>" . outline-hide-entry)
        ("M-<up>" . markdown-move-up)
        ("M-<down>" . markdown-move-down)
        ("C-c C-x C-v" . markdown-toggle-inline-images))
  :config
  (require 'org)
  ;; to support azure markdown format with =WxH after file link
  (setq markdown-regex-link-inline
        "\\(?1:!\\)?\\(?2:\\[\\)\\(?3:\\^?\\(?:\\\\\\]\\|[^]]\\)*\\|\\)\\(?4:\\]\\)\\(?5:(\\)\\s-*\\(?6:[^)]*?\\)\\(?:\\s-+\\(?7:\"[^\"]*\"\\|=.*\\)\\)?\\s-*\\(?8:)\\)")
  (defun markdown--browse-url (url)
    (let* ((struct (url-generic-parse-url url))
           (full (url-fullness struct))
           (file url))
      (message file)
      ;; Parse URL, determine fullness, strip query string
      (setq file (car (url-path-and-query struct)))
      (let ((file (if (and (s-starts-with? "/" file)
                           (not (file-exists-p file)))
                      (concat (project-root (project-current t))
                              file)
                    file)))
        ;; Open full URLs in browser, files in Emacs
        (if full
            (browse-url url)
          (when (and file (> (length file) 0))
            (let ((link-file (funcall markdown-translate-filename-function file)))
              (if (and markdown-open-image-command (string-match-p (image-file-name-regexp) link-file))
                  (if (functionp markdown-open-image-command)
                      (funcall markdown-open-image-command link-file)
                    (process-file markdown-open-image-command nil nil nil link-file))
                (find-file-other-window link-file))))))))

  (defun markdown-display-inline-images ()
    "Add inline image overlays to image links in the buffer.
This can be toggled with `markdown-toggle-inline-images'
or \\[markdown-toggle-inline-images]."
    (interactive)
    (unless (display-images-p)
      (error "Cannot show images"))
    (save-excursion
      (save-restriction
        (widen)
        (goto-char (point-min))
        (while (re-search-forward markdown-regex-link-inline nil t)
          (let* ((start (match-beginning 0))
                 (imagep (match-beginning 1))
                 (end (match-end 0))
                 (file (match-string-no-properties 6))
                 (file (if (s-starts-with? "/" file)
                           (concat (project-root (project-current t))
                                   file)
                         file)))
            (when (and imagep
                       (not (zerop (length file))))
              (unless (file-exists-p file)
                (let* ((download-file (funcall markdown-translate-filename-function file))
                       (valid-url (ignore-errors
                                    (member (downcase (url-type (url-generic-parse-url download-file)))
                                            markdown-remote-image-protocols))))
                  (if (and markdown-display-remote-images valid-url)
                      (setq file (markdown--get-remote-image download-file))
                    (when (not valid-url)
                      ;; strip query parameter
                      (setq file (replace-regexp-in-string "?.+\\'" "" file))
                      (unless (file-exists-p file)
                        (setq file (url-unhex-string file)))))))
              (when (file-exists-p file)
                (let* ((abspath (if (file-name-absolute-p file)
                                    file
                                  (concat default-directory file)))
                       (image
                        (cond ((and markdown-max-image-size
                                    (image-type-available-p 'imagemagick))
                               (create-image
                                abspath 'imagemagick nil
                                :max-width (car markdown-max-image-size)
                                :max-height (cdr markdown-max-image-size)))
                              (markdown-max-image-size
                               (create-image abspath nil nil
                                             :max-width (car markdown-max-image-size)
                                             :max-height (cdr markdown-max-image-size)))
                              (t (create-image abspath)))))
                  (when image
                    (let ((ov (make-overlay start end)))
                      (overlay-put ov 'display image)
                      (overlay-put ov 'face 'default)
                      (push ov markdown-inline-image-overlays))))))))))))
#+end_src

* Task Management
** Calendar

#+begin_src emacs-lisp
(when (not (boundp 'kimim/file-diary))
  (defvar kimim/file-diary (concat kimim/path-org "diary"))
  (if (not (file-exists-p kimim/file-diary))
      (write-region "" nil kimim/file-diary)))
#+end_src

#+begin_src emacs-lisp
(use-package calendar
  :defines (calendar-chinese-celestial-stem
            calendar-chinese-terrestrial-branch)
  :custom
  (diary-file kimim/file-diary)
  (calendar-latitude +30.16)
  (calendar-longitude +120.12)
  (calendar-location-name "Hangzhou")
  (calendar-remove-frame-by-deleting t)
  (calendar-week-start-day 1)
  (calendar-mark-holidays-flag t)
  (holiday-christian-holidays nil)
  (holiday-hebrew-holidays nil)
  (holiday-islamic-holidays nil)
  (holiday-solar-holidays nil)
  (holiday-bahai-holidays nil)
  (holiday-general-holidays
   '((holiday-fixed 1 1 "元旦")
     (holiday-float 5 0 2 "父親節")
     (holiday-float 6 0 3 "母親節")))
  (calendar-mark-diary-entries-flag t)
  (calendar-view-holidays-initially-flag nil)
  (calendar-chinese-celestial-stem
   ["甲" "乙" "丙" "丁" "戊" "己" "庚" "辛" "壬" "癸"])
  (calendar-chinese-terrestrial-branch
   ["子" "丑" "寅" "卯" "辰" "巳" "午" "未" "申" "酉" "戌" "亥"])
  :config
  ;; redefine Chinese sexagesimal format
  (defun calendar-chinese-sexagesimal-name (n)
    "The N-th name of the Chinese sexagesimal cycle.
N congruent to 1 gives the first name, N congruent to 2 gives the
second name, ..., N congruent to 60 gives the sixtieth name."
    (format
     "%s%s 年"
     (aref calendar-chinese-celestial-stem (% (1- n) 10))
     (aref calendar-chinese-terrestrial-branch (% (1- n) 12)))))
#+end_src

** org as GTD system

#+begin_src emacs-lisp
(use-package org-capture
  :ensure nil
  :custom
  (org-capture-templates
   '(("c" "Capture" entry (file+headline "capture.org" "Inbox")
      "* %?\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n")
     ("t" "TODO Task"    entry (file+headline "capture.org" "Inbox")
      "* TODO %?\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n")
     ("s" "SCHED Task"    entry (file+headline "capture.org" "Inbox")
      "* SCHED %?\nSCHEDULED: %t\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n")
     ("o" "OPEN Issue"  entry (file+headline "capture.org" "Inbox")
      "* OPEN %?\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n")
     ("w" "WAIT Task"    entry (file+headline "capture.org" "Inbox")
      "* WAIT %?\nSCHEDULED: %t\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n")
     ("h" "Habit"   entry (file+headline "global.org"   "Habit")
      "* %?  :habit:\n:PROPERTIES:\n:CAPTURED: %U\n:END:\n"))))
#+end_src

#+begin_src emacs-lisp
(use-package org
  :functions (org-agenda-kill-all-agenda-buffers
              org-agenda-todo
              org-agenda-error
              org-agenda-check-no-diary)
  :hook (org-agenda-mode . hl-line-mode)
  :defines org-agenda-mode-map
  :commands (org-toggle-office org-toggle-home org-toggle-home-or-office)
  :bind (("C-c a" . org-agenda)
         ("C-c c" . org-capture)
         :map org-agenda-mode-map
         ("C-c C-x C-p" . org-previous-link)
         ("C-c C-x C-n" . org-next-link)
         ("C-c C-k" . org-agenda-kill-files)
         ("<C-left>"  . org-agenda-do-date-earlier)
         ("<C-right>" . org-agenda-do-date-later)
         ("<S-left>" . (lambda ()
                         (interactive)
                         (org-agenda-todo 'left)))
         ("<S-right>" . (lambda ()
                          (interactive)
                          (org-agenda-todo 'right))))
  :custom
  (org-directory kimim/path-org)
  (org-agenda-files
   (file-expand-wildcards (concat kimim/path-org "*.org")))
  (org-tags-exclude-from-inheritance '("project" "category"))
  (org-log-done t)
  (org-fontify-done-headline nil)
  (org-todo-repeat-to-state "REPEAT")
  (org-deadline-warning-days 2)
  (org-todo-keywords
   '(
     ;; for tasks
     (sequence "TODO(t!)" "SCHED(s)" "|" "DONE(d@/!)")
     ;; for risks, actions, problems
     (sequence "OPEN(o!)" "WAIT(w@/!)" "|" "CLOSE(c@/!)")
     (sequence "|" "SOMEDAY(m)")
     (sequence "|" "ABORT(a@/!)")
     (sequence "REPEAT(r)" "|")))
  (org-tag-alist
   '(("@office" . ?o) ("@home" . ?h)
     ("team" . ?t) ("leader" . ?l)
     ("risk" . ?k)
     ("reading" . ?r) ("writing" . ?w)
     ("project" . ?p) ("category" . ?c)
     ("habit" . ?H)))
  (org-stuck-projects
   '("+LEVEL>=2-category-habit-info"
     ("TODO" "SCHED"  "DONE"
      "OPEN" "WAIT" "CLOSE"
      "ABORT" "SOMEDAY" "REPEAT")
     nil nil))
  (org-agenda-include-diary t)
  (org-agenda-span 2)
  (org-agenda-skip-scheduled-if-done t)
  (org-agenda-skip-deadline-if-done t)
  (org-agenda-custom-commands
   '(("t" todo "TODO|OPEN"
      ((org-agenda-sorting-strategy '(priority-down))))
     ("w" todo "SCHED|WAIT"
      ((org-agenda-sorting-strategy '(priority-down))))
     ;; all task should be done or doing
     ("d" todo "TODO|SCHED|OPEN|WAIT"
      ((org-agenda-sorting-strategy '(priority-down))))
     ("f" todo "SOMEDAY"
      ((org-agenda-sorting-strategy '(priority-down))))
     ("h" tags "habit/-ABORT-CLOSE"
      ((org-agenda-sorting-strategy '(todo-state-down))))
     ("c" tags "clock"
      ((org-agenda-sorting-strategy '(priority-down))))))
  :config
  (require 'org-capture)
  (require 'org-agenda)
  (add-hook 'kill-emacs-hook
            (lambda ()
              (org-clock-out nil t nil)
              (org-save-all-org-buffers)))
  ;; kill diary when exit agenda
  (advice-add 'org-agenda-exit
              :after (lambda () (kill-buffer "diary")))
  (diminish 'auto-fill-function)

  (setq org-refile-targets
        '(;; refile to maxlevel 2 of current file
          (nil . (:maxlevel . 1))
          ;; refile to maxlevel 1 of org-agenda-files
          (org-agenda-files :maxlevel . 1)
          ;; refile to item with 'project' tag in org-agenda-files
          (org-agenda-files :tag . "project")
          (org-agenda-files :tag . "category")))

  (defadvice org-schedule (after add-todo activate)
    (if (or (string= "OPEN" (org-get-todo-state))
            (string= "WAIT" (org-get-todo-state))
            (string= "CLOSE" (org-get-todo-state)))
        (org-todo "WAIT")
      (org-todo "SCHED")))

  (defadvice org-deadline (after add-todo activate)
    (if (or (string= "OPEN" (org-get-todo-state))
            (string= "WAIT" (org-get-todo-state))
            (string= "CLOSE" (org-get-todo-state)))
        (org-todo "WAIT")
      (org-todo "SCHED")))

  (defun org-agenda-kill-files ()
    (interactive)
    (org-agenda-kill-all-agenda-buffers)
    (mapcar (lambda (file)
              (if-let (buf (get-file-buffer file))
                  (kill-buffer buf)))
            org-agenda-files)))
#+end_src

* Mail and Contacts
** EBDB - a replacement for BBDB, as contact management

#+begin_src emacs-lisp
(use-package ebdb
  :functions ebdb-gethash
  :commands ebdb ebdb-mail-aliases
  :custom (ebdb-mua-pop-up nil)
  :config
  (setq ebdb-sources (concat kimim/path-org "ebdb"))
  (require 'ebdb-gnus)
  (require 'ebdb-message)
  (require 'ebdb-org)
  (add-hook 'message-setup-hook 'ebdb-mail-aliases)
  (setq org-link-make-description-function
      (lambda (link desc)
        (let* ((link-content (split-string link ":"))
               (key (car link-content))
               (link-str (cadr link-content))
               (link-uuid (cadr (split-string link-str "/"))))
          (if desc
              desc
            (if (string= "ebdb" key)
                (ebdb-record-name-string
                 (ebdb-gethash link-uuid 'uuid))))))))
#+end_src

** erc

#+begin_src emacs-lisp
;; erc settings
(use-package erc
  :functions erc-autojoin-enable
  :commands erc
  :custom
  (erc-autojoin-channels-alist
   '(("irc.freenode.net" "#emacs")))
  (erc-hide-list '("JOIN" "PART" "QUIT"))
  :config
  (require 'erc-join)
  (erc-autojoin-enable)
  (setq erc-default-server "irc.freenode.net"))
#+end_src

** GNUS dired

#+begin_src emacs-lisp
(use-package gnus-dired
  :ensure nil
  :commands (turn-on-gnus-dired-mode)
  :config
  ;; make the `gnus-dired-mail-buffers' function also work on
  ;; message-mode derived modes, such as mu4e-compose-mode
  (defun gnus-dired-mail-buffers ()
    "Return a list of active message buffers."
    (let (buffers)
      (save-current-buffer
        (dolist (buffer (buffer-list t))
          (set-buffer buffer)
          (when (and (derived-mode-p 'message-mode)
                     (null message-sent-message-via))
            (push (buffer-name buffer) buffers))))
      (nreverse buffers)))
  (setq gnus-dired-mail-mode 'mu4e-user-agent))
#+end_src

** mu4e

#+begin_src emacs-lisp
(use-package sendmail
  :ensure nil
  :custom
  (mail-user-agent 'sendmail-user-agent)
  (mail-signature nil)
  (mail-self-blind t)
  (mail-signature-file (concat kimim/path-sync-emacs "signature.txt")))
#+end_src

#+begin_src emacs-lisp
  (use-package mu-cite
    :commands (mu-cite-original)
    :config
    (setq mu-cite-top-format '("On " date ", " from " wrote:\n\n"))
    (setq mu-cite-prefix-format '(" > ")))
#+end_src

#+begin_src emacs-lisp
(eval-and-compile
  (defun mu4e-load-path ()
    (cond ((eq system-type 'darwin)
           "/usr/local/Cellar/mu/1.0_1/share/emacs/site-lisp/mu/mu4e")
          ((eq system-type 'windows-nt)
           "/usr/local/share/emacs/site-lisp/mu4e")
          ((eq system-type 'gnu/linux)
           "/usr/local/share/emacs/site-lisp/mu4e/"))))

(use-package mu4e
  :ensure nil
  :functions (mu4e-compose-reply
              mu4e~view-quit-buffer)
  :defines (mu4e-html2text-command
            mu4e-mu-binary
            mu4e-get-mail-command
            mu4e-update-interval
            mu4e-hide-index-messages
            mu4e-use-fancy-chars
            mu4e-view-show-images
            mu4e-view-fields
            mu4e-headers-fields
            mu4e-compose-cite-function
            mu4e-compose-reply-recipients
            mu4e-headers-mode-map
            mu4e-compose-mode-map
            mu4e-view-mode-map
            shr-color-visible-luminance-min
            shr-color-visible-distance-min)
  :custom
  (mu4e-compose-reply-recipients 'sender)
  (mu4e-compose-signature-auto-include nil)
  :commands (mu4e mu4e-compose-new)
  :bind (
         :map mu4e-headers-mode-map
         ("r" . kimim/mu4e-compose-reply-sender)
         ("R" . kimim/mu4e-compose-reply-all)
         ("f" . kimim/mu4e~view-quit-buffer)
         :map mu4e-compose-mode-map
         ("<C-tab>" . message-tab)
         :map mu4e-view-mode-map
         ("<home>" . move-beginning-of-line)
         ("<end>" . move-end-of-line)
         ("r" . kimim/mu4e-compose-reply-sender)
         ("R" . kimim/mu4e-compose-reply-all))
  :load-path (lambda () (list (mu4e-load-path)))
  :config
  (require 'sendmail)
  ;; turn html email to lighter color in dark theme
  (require 'mu4e-contrib)
  (setq mu4e-html2text-command 'mu4e-shr2text)
  (setq shr-color-visible-luminance-min 60)
  (setq shr-color-visible-distance-min 5)
  (setq shr-use-colors nil)
  (advice-add #'shr-colorize-region :around (defun shr-no-colourise-region (&rest ignore)))

  (require 'org-mu4e) ;; capture link
  (add-to-list 'Info-additional-directory-list "/usr/local/share/info")
  (setq mu4e-mu-binary "/usr/local/bin/mu")
  ;; (cond ((eq system-type 'gnu/linux)
  ;;        (setq mu4e-mu-binary "/snap/bin/mu")))
  (setq mail-user-agent 'mu4e-user-agent)
  ;; Fetch mail by offlineimap
  (setq mu4e-get-mail-command "offlineimap -c ~/.offlineimaprc -u quiet")
  ;; Fetch mail in 60 sec interval
  (setq mu4e-update-interval 300)
  ;; hide indexing messages from minibuffer
  (setq mu4e-hide-index-messages t)
  (setq mu4e-use-fancy-chars nil)
  (setq mu4e-view-show-images t)
  (setq mu4e-view-fields
        '(:subject :from :to :cc :date :mailing-list
                   :attachments :signature :decryption))
  (setq mu4e-headers-fields
        '( (:human-date    .   12)
           (:flags         .    6)
           (:from          .   22)
           (:subject       .   nil)))
  (setq mu4e-compose-cite-function 'mu-cite-original)
  (add-hook 'mu4e-view-mode-hook 'visual-line-mode)
  (add-hook 'mu4e-compose-mode-hook 'kimim/mail-setup)
  (add-hook 'mu4e-compose-mode-hook 'orgalist-mode)
  (add-hook 'mu4e-compose-mode-hook (lambda ()
                                      (auto-fill-mode -1)))
  (defun kimim/mu4e~view-quit-buffer ()
    (interactive)
    (when (get-buffer "*mu4e-view*")
      (switch-to-buffer "*mu4e-view*")
      (mu4e~view-quit-buffer)))

  (defun kimim/mu4e-compose-reply-sender ()
    (interactive)
    (set (make-local-variable 'mu4e-compose-reply-recipients) 'sender)
    (mu4e-compose-reply))

  (defun kimim/mu4e-compose-reply-all ()
    (interactive)
    (set (make-local-variable 'mu4e-compose-reply-recipients) 'all)
    (mu4e-compose-reply)))
#+end_src

* Utilities
** restclient

#+begin_src emacs-lisp
(use-package restclient
  :mode ("\\.http\\'" . restclient-mode)
  :bind (:map restclient-mode-map
         ("C-c C-c" . restclient-http-send-current-stay-in-window)
         ("C-c C-v" . restclient-http-send-current)))
#+end_src

** Reading News

#+begin_src emacs-lisp
(use-package elfeed
  :functions elfeed-show-refresh
  :commands (elfeed)
  :custom
  (elfeed-curl-timeout 100)
  (browse-url-browser-function 'browse-url-default-browser)
  (elfeed-feeds
   '(("http://kimi.im/atom.xml" blog)
     ("https://blog.tecosaur.com/tmio/rss.xml" Org)
     ("www.chinadaily.com.cn/rss/bizchina_rss.xml" CN)
     ;; from https://www.foxnews.com/story/foxnews-com-rss-feeds
     ("http://feeds.foxnews.com/foxnews/world" EN)
     ("http://feeds.foxnews.com/foxnews/scitech" EN Tech)
     ;; from https://www.lefigaro.fr/rss
     ("https://www.lefigaro.fr/rss/figaro_secteur_high-tech.xml" FR Hightech)
     ("https://www.lefigaro.fr/rss/figaro_management.xml" FR Management)
     ;; from https://www.zeit.de/hilfe/hilfe#rss
     ("http://newsfeed.zeit.de/wissen/index" DE Wissen)
     ("http://newsfeed.zeit.de/kultur/index" DE Kultur)))
  :bind (:map elfeed-search-mode-map
         ("<SPC>" . scroll-up-command)
         ("<backspace>" . scroll-down-command)
         :map elfeed-show-mode-map
         ("M-q" . article-fill-long-lines)
         ("j" . next-line)
         ("k" . previous-line))
  :config
  (require 'org)
  ;; open feed link with eww
  (require 'gnus-art)
  (advice-add 'elfeed-show-entry :after
              (lambda (entry)
                (setq line-spacing 0.4)
                (elfeed-show-refresh))))
#+end_src

** Encryption

Sometimes, you need to encrypt some secret files, setting ~epa-pinentry-mode~ to
~loopback~ will prompt password inside minibuffer, while not show a dialog for it.

And we also cache the symmetric key in the same
#+begin_src emacs-lisp
(use-package epa
  :ensure nil
  :custom
  (epa-pinentry-mode 'loopback)
  (epa-file-cache-passphrase-for-symmetric-encryption t))
#+end_src

** Viewing Documents

doc-view-mode can view many kind of documents, such as PDF, PS and images. You
should install postscript in cygwin.

#+begin_src emacs-lisp
(use-package doc-view
  :custom
  (doc-view-continuous t)
  (doc-view-image-width 500)
  (doc-view-resolution 300))
#+end_src

* kimim utils

In Windows environment, =kimim/xterm= and =kimim/dc= will look up the program
from system PATH, so you should set these to system PATH.

#+begin_src emacs-lisp
(use-package kimim
  :load-path "~/kimim-emacs/site-lisp"
  :ensure nil
  :commands (kimim/mail-setup)
  :defines (mac-option-modifier
            mac-command-modifier)
  :bind
  (("C-x m y" . kimim/mail-attach-files)
   ("<f9>" . kimim/xterm)
   ("S-<f9>" . kimim/cmd)
   ;; when deploy clojars, need msys2 term to use pinetry
   ("C-<f9>" . kimim/msys2-term)
   ("C-c r" . kimim/rename-file-and-buffer)
   ("C-x m o" . kimim/open-external)
   ("C-x m O" . kimim/open-external-pdf)
   ("C-x m d" . kimim/dc)
   ("C-c d" . kimim/lookinsight)
   ("<f1>" . delete-other-windows)
   ("C-<f1>" . nuke-other-buffers)
   ("M-<f1>" . kimim/switch-to-scratch-and-nuke-others)
   ("<f2>" . other-window)
   ("<f5>" . kimim/switch-to-scratch-buffer)
   ("<f7>" . bury-buffer)
   ("<f8>" . unbury-buffer)
   ("C-h" . delete-backward-char)
   ("M-h" . backward-kill-word)
   ("M-?" . mark-paragraph)
   ("C-x k" . kill-current-buffer)
   ("C-x C-v" . view-file-other-window)
   ("C-c C-o" . occur)
   ("C-z" . set-mark-command)
   ("M-n" . next-error)
   ("M-p" . previous-error)
   ("C-c C-/" . comment-or-uncomment-region)
   ("RET" . newline-and-indent)
   ("C-x m h" . help)
   ("C-x m ." . unbury-buffer)
   ("C-x m ," . bury-buffer)
   ("C-x m  。" . unbury-buffer)
   ("C-x m  ，" . bury-buffer)
   ("C-x ," . bury-buffer)
   ("C-x ." . unbury-buffer)
   ("C-x  ，" . bury-buffer)
   ("C-x  。" . unbury-buffer)
   ("C-x  ‘" . hippie-expand)
   ("C-x  ’" . hippie-expand)
   ("C-x M-s" . eshell)
   ("C-x /" . kimim/toggle-path-header)
   ("C-x m 0" . kimim/frame-and-font)
   ("C-x m SPC" . kimim/shrink-down)
   ("C-x m ]" . kimim/shrink-right)
   ("C-x m [" . kimim/shrink-left)
   ("M-]" . kimim/shrink-right)
   ("M-[" . kimim/shrink-left)
   ("C-x m ^" . kimim/shrink-up)
   ("C-x m '" . kimim/top-right-mouse)
   ("C-x m r" . kimim/rename-file-and-buffer))
  :config
  (unbind-key "C-x C-z")
  (when (eq system-type 'darwin) ;; mac specific settings
    (setq mac-option-modifier 'meta)
    (setq mac-command-modifier 'meta)
    ;; sets fn-delete to be right-delete
    (global-set-key [kp-delete] 'delete-char))
  (defun kimim/switch-to-scratch-and-nuke-others ()
    (interactive)
    (switch-to-buffer "*scratch*") (nuke-other-buffers))
  (defun kimim/switch-to-scratch-buffer ()
    (interactive)
    (switch-to-buffer "*scratch*")
    (delete-other-windows))
  (defun kimim/restore-text-scale ()
    (interactive)
    (text-scale-increase 0)))
#+end_src

* Footnotes

[fn:1] http://www.literateprogramming.com/

[fn:2] https://orgmode.org/

[fn:3] https://www.msys2.org/

[fn:4] http://cygwin.com/

[fn:5] https://cygwin.com/setup-x86_64.exe

[fn:6] http://kimi.im/2021-01-28-emacs-inside-manjaro-wsl2-windows

[fn:7] http://brew.sh/

[fn:8] https://www.gnu.org/software/emacs/manual/html_node/emacs/Packages.html

[fn:9] https://github.com/syl20bnr/spacemacs/issues/381

[fn:10] https://github.com/Fuco1/smartparens

[fn:11] https://github.com/emacs-lsp/lsp-mode/issues/2622

[fn:12] https://clojure.org/

[fn:13] https://cider.mx/

[fn:14] https://github.com/jorgenschaefer/elpy

[fn:15] https://github.com/ralesi/ahk-mode

[fn:16] https://mermaid-js.github.io/

[fn:17] https://zettelkasten.de/

[fn:18] https://en.wikipedia.org/wiki/Niklas_Luhmann

[fn:19] https://pandoc.org/

[fn:22] https://github.com/transcode-open/apt-cyg

[fn:21] https://emacs.stackexchange.com/questions/22283/dired-group-directories-and-symlinks-to-directories-first

[fn:20] https://www.gnu.org/software/emacs/manual/html_node/elisp/File-Name-Components.html
